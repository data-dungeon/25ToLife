.vu

include( `VUDefines.vm4')
include( 'VUStaticDefines.vm4')

define( 'szeDblBuffer', eval( (szeVUMemAvailable) / 2))

define( 'irKickPtr', 'VI10')
define( 'irLightingFunc', 'VI12')

	.global	dbVertexColorSetCompress2AllCore2Clip_CodeBegin
	.global	dbVertexColorSetCompress2AllCore2Clip_CodeEnd

.dmadata dbVertexColorSetCompress2AllCore2Clip_CodeBegin
	NOP		XTOP	irKickPtr	; get ptr to VUBuffer_1
	NOP		LOI	16.0

	NOP		LQ	VF09, 0(irKickPtr)	; Load CommonToClipMtx row 1
	NOP		LQ	VF10, 1(irKickPtr)	; Load CommonToClipMtx row 2
	NOP		LQ	VF11, 2(irKickPtr)	; Load CommonToClipMtx row 3
	NOP		LQ	VF12, 3(irKickPtr)	; Load CommonToClipMtx row 4
	NOP		LQ	VF13, 4(irKickPtr)	; Load ObjToCommonMtx row1
	NOP		LQ	VF14, 5(irKickPtr)	; Load ObjToCommonMtx row1
	NOP		LQ	VF15, 6(irKickPtr)	; Load ObjToCommonMtx row1
	NOP		LQ	VF16, 7(irKickPtr)	; Load ObjToCommonMtx row1

	NOP		LQ	VF20,12(irKickPtr)	; Load ClipToScreen Values1
	NOP		LQ	VF21,13(irKickPtr)	; Load ClipToScreen Values2
	NOP		LQ	VF01,14(irKickPtr)	; Load Core2 CtrlData	
	
	NOP		LQ	VF27, 8(irKickPtr)	; Load FogData { FogNear, FogFar, FogScale, FogConst }
	NOP		LQ	VF28, 9(irKickPtr)	; Load TextureMtx1
	NOP		LQ	VF29,10(irKickPtr)	; Load TextureMtx2
	MULi.zw	VF21, VF21, I	LQ	VF30,11(irKickPtr)	; Load TextureMtx3 { w = alphaMul }

	NOP		MTIR	VI02, VF01x	; Load Setup Addr
	NOP		JALR	VI15, VI02
	NOP		IADDIU	VI01, irKickPtr, 15
	
	NOP[E]		LQ	VF31, 0(VI01)	; Load GIFTag
	NOP		IADDIU	irLightingFunc, VI02, 0x00
		
; load the nop giftag into addr 0
	NOP		IADDIU	VI01, VI00, 0x7fff
	NOP		IADDIU	VI01, VI01, 0x0001
	NOP		ISW.x  	VI01, 0(VI00)x
	
dbVCSC_GetRelAddr:

	NOP		BAL	VI15, dbVCSC_ConcatMatrices
	NOP		NOP
dbVCSC_ConcatMatrices:

; Concatinate ObjToCommon * CommonToClip and build up ClipToScreen
	MULAx.xyzw	ACC, VF09, VF13x	XTOP	VI01
	MADDAy.xyzw	ACC, VF10, VF13y	ISUBIU	VI15, VI15, dbVCSC_ConcatMatrices
	MADDAz.xyzw	ACC, VF11, VF13z	NOP
	MADDw.xyzw	VF01, VF12, VF13w	LOI	2048.0
	
	MULAx.xyzw	ACC, VF09, VF14x	MR32.w	VF05, VF00
	MADDAy.xyzw	ACC, VF10, VF14y	MOVE.x	VF05, VF21
	MADDAz.xyzw	ACC, VF11, VF14z	MOVE.yz	VF05, VF00
	MADDw.xyzw	VF02, VF12, VF14w	MOVE.y	VF06, VF21

	MULAx.xyzw	ACC, VF09, VF15x	MOVE.xz	VF06, VF00
	MADDAy.xyzw	ACC, VF10, VF15y	MR32.w	VF06, VF00
	MADDAz.xyzw	ACC, VF11, VF15z	MOVE.z	VF07, VF21
	MADDw.xyzw	VF03, VF12, VF15w	MOVE.xy	VF07, VF00

	MULAx.xyzw	ACC, VF09, VF16x	MR32.w	VF07, VF00
	MADDAy.xyzw	ACC, VF10, VF16y	MR32.z	VF08, VF21
	MADDAz.xyzw	ACC, VF11, VF16z	MOVE.w	VF08, VF00
	MADDw.xyzw	VF04, VF12, VF16w	MOVE.xy	VF08, VF20

	ADDx.z	VF28, VF00, VF27	NOP
	ADDw.x	VF27, VF00, VF30	IADDIU	VI01, irKickPtr, 0
	MULy.w	VF28, VF00, VF27	MTIR	VI13, VF20z		; grab ptrClipSetup( )
	MULz.w	VF29, VF00, VF27	IADDIU	VI05, VI15, dbVCSC_OutputClippedFan
	
	MULw.w	VF30, VF27, VF00	JALR	VI15, VI13
	NOP		IADDIU	VI02, VI00, szeDblBuffer		; <ds>
		
	NOP		MTIR	VI01, VF20w		; grab ptrClipBuffer( )
	
	NOP		ISW.z	VI13, -1(VI14)z
	NOP		ISW.w	VI01, -1(VI14)w
	
	NOP		SQ	VF27, -4(VI14)
	NOP		SQ	VF28, -5(VI14)

	NOP		XGKICK	VI00
	NOP[E]		NOP
	NOP		NOP

define( 'irBaseVtxPtr', 'VI02')
define( 'irClrPtr', 'VI05')
define( 'irNrmPtr', 'VI13')
define( 'irCopyCount', 'VI07')
define( 'irVtxCount', 'VI08')
define( 'irLoopCount', 'VI09')

dbVCSC_Start:
	NOP		XTOP	irBaseVtxPtr		; get ptr to VUBuffer

	NOP		ILW.y	irNrmPtr, 1(irBaseVtxPtr)y		; grab offsetNormals
	NOP		ILW.x	irClrPtr, 1(irBaseVtxPtr)x		; grab offsetColorStream
	
	NOP		ILW.z	irCopyCount, 0(irBaseVtxPtr)z		; grab copyCount
	NOP		ILW.x	irVtxCount, 0(irBaseVtxPtr)x		; grab vertexCount
	NOP		ILW.y	irLoopCount, 0(irBaseVtxPtr)y		; grab loopCount

	NOP		IADD	irNrmPtr, irBaseVtxPtr, irNrmPtr
	NOP		IADD	irClrPtr, irBaseVtxPtr, irClrPtr
	NOP		IADDIU	irBaseVtxPtr, irBaseVtxPtr, 2		; skip BufferHdr

	NOP		IBEQ	irCopyCount, VI00, dbVCSC_NoGIFData		; skip GIFPacket copy if count == 0
	NOP		IADDIU	VI15, VI00, 0x7fff

dbVCSC_GIFDataSetup:
	NOP		ILW.x	VI01, 0(irBaseVtxPtr)x

	NOP		IADD	irBaseVtxPtr, irBaseVtxPtr, irCopyCount		; calculate CopyEnd ptr
	NOP		ISUB	irCopyCount, irBaseVtxPtr, irCopyCount

	NOP		IADDIU	VI15, VI15, 0x0001
	NOP		IOR	VI01, VI01, VI15

	NOP		ISW.x	VI01,  0(irCopyCount)x
	NOP		NOP
	NOP		NOP
	NOP		NOP

	NOP		XGKICK	irCopyCount

dbVCSC_NoGIFData:

define( 'irTemp1', 'VI01')

define( 'irVtxPtr', 'irBaseVtxPtr')
define( 'irZADCPtr', 'VI03')
define( 'irEndPtr', 'VI04')
define( 'irADCBits', 'VI06')
define( 'irClipFlag2', 'VI07')
define( 'irClipFlag1', 'VI08')
define( 'irClipFlag3', 'VI09')
define( 'irADCMask', 'VI10')
define( 'irVtxClipMask', 'VI11')

define( 'offLitRGBAs', eval(-10))
define( 'irLitRGBAs', 'VI14')

dbVCSC_Prolog:
	NOP		IADD	irZADCPtr, irVtxPtr, irVtxCount		; set stream ptrs

	NOP		ISUBIU	irTemp1, irLoopCount, 1		; set InnerLoop count
	NOP		IADD	irTemp1, irTemp1, irTemp1		; irTemp2 = LoopCnt * 2
	NOP		IADD	irTemp1, irTemp1, irTemp1		; irTemp2 = LoopCnt * 4
	NOP		IADD	irEndPtr, irVtxPtr, irTemp1		; set EndLoop ptr
	NOP		IADDIU	irEndPtr, irEndPtr, 2		; add 2 for pre-loads

	NOP		IADD	VI10, irVtxPtr, irVtxCount		; set EndAll ptr
	NOP		IADDIU	VI10, VI10, 2		; add 2 for pre-loads

	NOP		LQI	VF09, (irVtxPtr++)		; grab first vtx
	NOP		LQI	VF13, (irZADCPtr++)		; grab first ZADC Data
	NOP		ISW.w	VI10, -3(VI14)w
	MULAw.xyzw	ACC, VF04, VF00w 	NOP
	MADDAz.xyzw	ACC, VF01, VF09z	NOP
	MADDAw.xyzw	ACC, VF02, VF09w	LQI	VF10, (irVtxPtr++)		; grab second Vtx
	MADDx.xyzw	VF14, VF03, VF13x	NOP

	NOP		ISUB	VI01, VI10, irEndPtr
	NOP		MTIR	irADCBits, VF13x
	NOP		IBLTZ	VI01, dbVCSC_Epilog
	NOP		IADDIU	irVtxClipMask, VI00, 0x003f

dbVCSC_Loop:
	ADDx.z	VF26, VF00, VF13x	LQI	VF27, (irNrmPtr++)
	ADDx	VF25, VF14, VF00x	MOVE	VF24, VF09
	ADDw.y	VF26, VF00, VF09w	JALR	VI15, irLightingFunc
	ADDz.x	VF26, VF00, VF09z	LQI	VF28, (irClrPtr++)

	ADDx.xy	VF18, VF11, VF00x	IADDIU	irADCMask, VI00, 0x01
	MULAw.xyzw	ACC, VF04, VF00w	LQI	VF11, (irVtxPtr++)
	CLIPw.xyz	VF14, VF14w	IAND	VI01, irADCBits, irADCMask
	MADDAz.xyzw	ACC, VF01, VF10z	IAND	irClipFlag1, irClipFlag2, irVtxClipMask
	MADDAw.xyzw	ACC, VF02, VF10w	IBNE	VI01, VI00, dbCheckVtx2
	MADDy.xyzw	VF15, VF03, VF13y	FCGET	irClipFlag2
dbProcessTri341:
	ADDx.xyzw	VF19, VF12, VF00x	FCAND	VI01, 0x3ffff
	ADDx.xyzw	VF20, VF09, VF00x	IBNE	VI01, VI00, dbTri341NotAllIn
	ADDx.xyzw	VF24, VF16, VF00x	FCGET	irClipFlag3
	
dbTri341AllIn:
	ADDx.xyzw	VF25, VF17, VF00x	B	BucketTri
	ADDx.xyzw	VF26, VF14, VF00x	IADDIU	VI15, VI00, dbCheckVtx2			
	
dbTri341NotAllIn:
	ADDx.xyzw	VF25, VF17, VF00x	IAND	VI01, irClipFlag1, irClipFlag2
	ADDx.xyzw	VF26, VF14, VF00x	IAND	VI01, VI01, irClipFlag3
	NOP		NOP
	NOP		IBEQ	VI01, VI00, ClipTri
	NOP		IADDIU	VI15, VI00, dbReturnClip341

dbReturnClip341:
	CLIPw.xyz	VF17, VF17w	NOP
	CLIPw.xyz	VF14, VF14w	IADDIU	irVtxClipMask, VI00, 0x003f		
		
dbCheckVtx2:
	ADDy.z	VF26, VF00, VF13y	LQI	VF27, (irNrmPtr++)
	ADDx	VF25, VF15, VF00x	MOVE	VF24, VF10
	ADDw.y	VF26, VF00, VF10w	JALR	VI15, irLightingFunc
	ADDz.x	VF26, VF00, VF10z	LQI	VF28, (irClrPtr++)

	ADDx.xy	VF18, VF12, VF00x	IADDIU	irADCMask, VI00, 0x02
	MULAw.xyzw	ACC, VF04, VF00w	LQI	VF12, (irVtxPtr++)
	CLIPw.xyz	VF15, VF15w	IAND	VI01, irADCBits, irADCMask
	MADDAz.xyzw	ACC, VF01, VF11z	IAND	irClipFlag1, irClipFlag2, irVtxClipMask
	MADDAw.xyzw	ACC, VF02, VF11w	IBNE	VI01, VI00, dbCheckVtx3
	MADDz.xyzw	VF16, VF03, VF13z	FCGET	irClipFlag2
dbProcessTri412:
	ADDx.xyzw	VF19, VF09, VF00x	FCAND	VI01, 0x3ffff
	ADDx.xyzw	VF20, VF10, VF00x	IBNE	VI01, VI00, dbTri412NotAllIn
	ADDx.xyzw	VF24, VF17, VF00x	FCGET	irClipFlag3
	
dbTri412AllIn:
	ADDx.xyzw	VF25, VF14, VF00x	B	BucketTri
	ADDx.xyzw	VF26, VF15, VF00x  	IADDIU	VI15, VI00, dbCheckVtx3
	
dbTri412NotAllIn:
	ADDx.xyzw	VF25, VF14, VF00x	IAND	VI01, irClipFlag1, irClipFlag2
	ADDx.xyzw	VF26, VF15, VF00x	IAND	VI01, VI01, irClipFlag3
	NOP		NOP
	NOP		IBEQ	VI01, VI00, ClipTri
	NOP		IADDIU	VI15, VI00, dbReturnClip412

dbReturnClip412:
	CLIPw.xyz	VF14, VF14w	NOP
	CLIPw.xyz	VF15, VF15w	IADDIU	irVtxClipMask, VI00, 0x003f		; set ADCMask4

dbCheckVtx3:
	ADDz.z	VF26, VF00, VF13z	LQI	VF27, (irNrmPtr++)
	ADDx	VF25, VF16, VF00x	MOVE	VF24, VF11
	ADDw.y	VF26, VF00, VF11w	JALR	VI15, irLightingFunc
	ADDz.x	VF26, VF00, VF11z	LQI	VF28, (irClrPtr++)
	
	ADDx.xy	VF18, VF09, VF00x	IADDIU	irADCMask, VI00, 0x04
	MULAw.xyzw	ACC, VF04, VF00w	LQI	VF09, (irVtxPtr++)
	CLIPw.xyz	VF16, VF16w	IAND	VI01, irADCBits, irADCMask
	MADDAz.xyzw	ACC, VF01, VF12z	IAND	irClipFlag1, irClipFlag2, irVtxClipMask
	MADDAw.xyzw	ACC, VF02, VF12w	IBNE	VI01, VI00, dbCheckVtx4
	MADDw.xyzw	VF17, VF03, VF13w	FCGET	irClipFlag2

dbProcessTri123:
	ADDx.xyzw	VF19, VF10, VF00x	FCAND	VI01, 0x3ffff
	ADDx.xyzw	VF20, VF11, VF00x	IBNE	VI01, VI00, dbTri123NotAllIn
	ADDx.xyzw	VF24, VF14, VF00x	FCGET	irClipFlag3
	
dbTri123AllIn:
	ADDx.xyzw	VF25, VF15, VF00x	B	BucketTri
	ADDx.xyzw	VF26, VF16, VF00x	IADDIU	VI15, VI00, dbCheckVtx4
	
dbTri123NotAllIn:
	ADDx.xyzw	VF25, VF15, VF00x	IAND	VI01, irClipFlag1, irClipFlag2
	ADDx.xyzw	VF26, VF16, VF00x	IAND	VI01, VI01, irClipFlag3
	NOP		NOP
	NOP		IBEQ	VI01, VI00, ClipTri
	NOP		IADDIU	VI15, VI00, dbReturnClip123

dbReturnClip123:
	CLIPw.xyz	VF15, VF15w	NOP
	CLIPw.xyz	VF16, VF16w	IADDIU	irVtxClipMask, VI00, 0x003f		; set ADCMask4

dbCheckVtx4:
	ADDw.z	VF26, VF00, VF13w	LQI	VF27, (irNrmPtr++)
	ADDx	VF25, VF17, VF00x	MOVE	VF24, VF12
	ADDw.y	VF26, VF00, VF12w	JALR	VI15, irLightingFunc
	ADDz.x	VF26, VF00, VF12z	LQI	VF28, (irClrPtr++)

	NOP		LQI	VF13, (irZADCPtr++)

	ADDx.xy	VF18, VF10, VF00x	IADDIU	irADCMask, VI00, 0x08
	MULAw.xyzw	ACC, VF04, VF00w	LQI	VF10, (irVtxPtr++)
	CLIPw.xyz	VF17, VF17w	IAND	VI01, irADCBits, irADCMask
	MADDAz.xyzw	ACC, VF01, VF09z	IAND	irClipFlag1, irClipFlag2, irVtxClipMask
	MADDAw.xyzw	ACC, VF02, VF09w	IBNE	VI01, VI00, dbVCSC_CheckFinish
	MADDx.xyzw	VF14, VF03, VF13x	FCGET	irClipFlag2

dbProcessTri234:
	ADDx.xyzw	VF19, VF11, VF00x	FCAND	VI01, 0x3ffff
	ADDx.xyzw	VF20, VF12, VF00x	IBNE	Vi01, VI00, dbTri234NotAllIn
	ADDx.xyzw	VF24, VF15, VF00x	FCGET	irClipFlag3
	
dbTri234AllIn:
	ADDx.xyzw	VF25, VF16, VF00x	B	BucketTri
	ADDx.xyzw	VF26, VF17, VF00x	IADDIU	VI15, VI00, dbVCSC_CheckFinish
	
dbTri234NotAllIn:
	ADDx.xyzw	VF25, VF16, VF00x	IAND	VI01, irClipFlag1, irClipFlag2
	ADDx.xyzw	VF26, VF17, VF00x	IAND	VI01, VI01, irClipFlag3
	NOP		NOP
	NOP		IBEQ	VI01, VI00, ClipTri
	NOP		IADDIU	VI15, VI00, dbReturnClip234

dbReturnClip234:
	CLIPw.xyz	VF16, VF16w	NOP
	CLIPw.xyz	VF17, VF17w	IADDIU	irVtxClipMask, VI00, 0x003f		; set ADCMask4
	
dbVCSC_CheckFinish:
	NOP		IBNE	irVtxPtr, irEndPtr, dbVCSC_Loop
	NOP		MTIR	irADCBits, VF13x

	NOP		ILW.w	irEndPtr, -3(VI14)w

dbVCSC_Epilog:
	NOP		IBEQ	irVtxPtr, irEndPtr, dbVCSC_End
	NOP		NOP

	ADDx.z	VF26, VF00, VF13x	LQI	VF27, (irNrmPtr++)
	ADDx	VF25, VF14, VF00x	MOVE	VF24, VF09
	ADDw.y	VF26, VF00, VF09w	JALR	VI15, irLightingFunc
	ADDz.x	VF26, VF00, VF09z	LQI	VF28, (irClrPtr++)

	ADDx.xy	VF18, VF11, VF00x	IADDIU	irADCMask, VI00, 0x01
	MULAw.xyzw	ACC, VF04, VF00w	LQI	VF11, (irVtxPtr++)
	CLIPw.xyz	VF14, VF14w	IAND	VI01, irADCBits, irADCMask
	MADDAz.xyzw	ACC, VF01, VF10z	IAND	irClipFlag1, irClipFlag2, irVtxClipMask
	MADDAw.xyzw	ACC, VF02, VF10w	IBNE	VI01, VI00, dbCheckEndVtx2
	MADDy.xyzw	VF15, VF03, VF13y	FCGET	irClipFlag2
dbProcessEndTri341:
	ADDx.xyzw	VF19, VF12, VF00x	FCAND	VI01, 0x3ffff
	ADDx.xyzw	VF20, VF09, VF00x	IBNE	VI01, VI00, dbEndTri341NotAllIn
	ADDx.xyzw	VF24, VF16, VF00x	FCGET	irClipFlag3
	
dbEndTri341AllIn:
	ADDx.xyzw	VF25, VF17, VF00x	B	BucketTri
	ADDx.xyzw	VF26, VF14, VF00x	IADDIU	VI15, VI00, dbCheckEndVtx2			
	
dbEndTri341NotAllIn:
	ADDx.xyzw	VF25, VF17, VF00x	IAND	VI01, irClipFlag1, irClipFlag2
	ADDx.xyzw	VF26, VF14, VF00x	IAND	VI01, VI01, irClipFlag3
	NOP		NOP
	NOP		IBEQ	VI01, VI00, ClipTri
	NOP		IADDIU	VI15, VI00, dbReturnClipEnd341

dbReturnClipEnd341:
	CLIPw.xyz	VF17, VF17w	NOP
	CLIPw.xyz	VF14, VF14w	IADDIU	irVtxClipMask, VI00, 0x003f		; set ADCMask4
		
dbCheckEndVtx2:
	NOP		IBEQ	irVtxPtr, irEndPtr, dbVCSC_End
	NOP		NOP
	
	ADDy.z	VF26, VF00, VF13y	LQI	VF27, (irNrmPtr++)
	ADDx	VF25, VF15, VF00x	MOVE	VF24, VF10
	ADDw.y	VF26, VF00, VF10w	JALR	VI15, irLightingFunc
	ADDz.x	VF26, VF00, VF10z	LQI	VF28, (irClrPtr++)

	ADDx.xy	VF18, VF12, VF00x	IADDIU	irADCMask, VI00, 0x02

	MULAw.xyzw	ACC, VF04, VF00w	LQI	VF12, (irVtxPtr++)
	CLIPw.xyz	VF15, VF15w	IAND	VI01, irADCBits, irADCMask
	MADDAz.xyzw	ACC, VF01, VF11z	IAND	irClipFlag1, irClipFlag2, irVtxClipMask
	MADDAw.xyzw	ACC, VF02, VF11w	IBNE	VI01, VI00, dbCheckEndVtx3
	MADDz.xyzw	VF16, VF03, VF13z	FCGET	irClipFlag2

dbProcessEndTri412:
	ADDx.xyzw	VF19, VF09, VF00x	FCAND	VI01, 0x3ffff
	ADDx.xyzw	VF20, VF10, VF00x	IBNE	VI01, VI00, dbEndTri412NotAllIn
	ADDx.xyzw	VF24, VF17, VF00x	FCGET	irClipFlag3
	
dbEndTri412AllIn:
	ADDx.xyzw	VF25, VF14, VF00x	B	BucketTri
	ADDx.xyzw	VF26, VF15, VF00x	IADDIU	VI15, VI00, dbCheckEndVtx3
	
dbEndTri412NotAllIn:
	ADDx.xyzw	VF25, VF14, VF00x	IAND	VI01, irClipFlag1, irClipFlag2
	ADDx.xyzw	VF26, VF15, VF00x	IAND	VI01, VI01, irClipFlag3
	NOP		NOP
	NOP		IBEQ	VI01, VI00, ClipTri
	NOP		IADDIU	VI15, VI00, dbReturnClipEnd412

dbReturnClipEnd412:
	CLIPw.xyz	VF14, VF14w	NOP
	CLIPw.xyz	VF15, VF15w	IADDIU	irVtxClipMask, VI00, 0x003f		; set ADCMask4

dbCheckEndVtx3:
	NOP		IBEQ	irVtxPtr, irEndPtr, dbVCSC_End
	NOP		NOP
	
	ADDz.z	VF26, VF00, VF13z	LQI	VF27, (irNrmPtr++)
	ADDx	VF25, VF16, VF00x	MOVE	VF24, VF11
	ADDw.y	VF26, VF00, VF11w	JALR	VI15, irLightingFunc
	ADDz.x	VF26, VF00, VF11z	LQI	VF28, (irClrPtr++)

	ADDx.xy	VF18, VF09, VF00x	IADDIU	irADCMask, VI00, 0x04

	MULAw.xyzw	ACC, VF04, VF00w	IADDIU	irVtxPtr, irVtxPtr, 1
	CLIPw.xyz	VF16, VF16w	IAND	VI01, irADCBits, irADCMask
	MADDAz.xyzw	ACC, VF01, VF12z	IAND	irClipFlag1, irClipFlag2, irVtxClipMask
	MADDAw.xyzw	ACC, VF02, VF12w	IBNE	VI01, VI00, dbCheckEndVtx4
	MADDw.xyzw	VF17, VF03, VF13w	FCGET	irClipFlag2

dbProcessEndTri123:
	ADDx.xyzw	VF19, VF10, VF00x	FCAND	VI01, 0x3ffff
	ADDx.xyzw	VF20, VF11, VF00x	IBNE	VI01, VI00, dbEndTri123NotAllIn
	ADDx.xyzw	VF24, VF14, VF00x	FCGET	irClipFlag3
	
dbEndTri123AllIn:
	ADDx.xyzw	VF25, VF15, VF00x	B	BucketTri
	ADDx.xyzw	VF26, VF16, VF00x	IADDIU	VI15, VI00, dbCheckEndVtx4
	
dbEndTri123NotAllIn:
	ADDx.xyzw	VF25, VF15, VF00x	IAND	VI01, irClipFlag1, irClipFlag2
	ADDx.xyzw	VF26, VF16, VF00x	IAND	VI01, VI01, irClipFlag3
	NOP		NOP
	NOP		IBEQ	VI01, VI00, ClipTri
	NOP		IADDIU	VI15, VI00, dbReturnClipEnd123

dbReturnClipEnd123:
	CLIPw.xyz	VF15, VF15w	NOP
	CLIPw.xyz	VF16, VF16w	IADDIU	irVtxClipMask, VI00, 0x003f		; set ADCMask4

dbCheckEndVtx4:
	NOP		IBEQ	irVtxPtr, irEndPtr, dbVCSC_End
	NOP		NOP
	
	ADDw.z	VF26, VF00, VF13w	LQI	VF27, (irNrmPtr++)
	ADDx	VF25, VF17, VF00x	MOVE	VF24, VF12
	ADDw.y	VF26, VF00, VF12w	JALR	VI15, irLightingFunc
	ADDz.x	VF26, VF00, VF12z	LQI	VF28, (irClrPtr++)

	ADDx.xy	VF18, VF10, VF00x	IADDIU	irADCMask, VI00, 0x08

	NOP		IADDIU	irVtxPtr, irVtxPtr, 1
	CLIPw.xyz	VF17, VF17w	IAND	VI01, irADCBits, irADCMask
	NOP		IAND	irClipFlag1, irClipFlag2, irVtxClipMask
	NOP		IBNE	VI01, VI00, dbVCSC_End
	NOP		FCGET	irClipFlag2

dbProcessEndTri234:
	ADDx.xyzw	VF19, VF11, VF00x	FCAND	VI01, 0x3ffff
	ADDx.xyzw	VF20, VF12, VF00x	IBNE	VI01, VI00, dbEndTri234NotAllIn
	ADDx.xyzw	VF24, VF15, VF00x	FCGET	irClipFlag3
	
dbEndTri234AllIn:
	ADDx.xyzw	VF25, VF16, VF00x	B	BucketTri
	ADDx.xyzw	VF26, VF17, VF00x	IADDIU	VI15, VI00, dbVCSC_End
	
dbEndTri234NotAllIn:
	ADDx.xyzw	VF25, VF16, VF00x	IAND	VI01, irClipFlag1, irClipFlag2
	ADDx.xyzw	VF26, VF17, VF00x	IAND	VI01, VI01, irClipFlag3
	NOP		NOP
	NOP		IBEQ	VI01, VI00, ClipTri
	NOP		IADDIU	VI15, VI00, dbVCSC_End

dbVCSC_End:

; TJC - kick nop giftag incase we didnt have anything to kick
	NOP		XGKICK	VI00

	NOP[E]		NOP
	NOP		NOP	
	NOP		B	dbVCSC_Start
	NOP		NOP

;-----------------------------------------------------------------------------------------------------------------------------------------------
BucketTri:
	NOP		BAL	VI09, BucketTriRefAddr
	NOP		ILW.x	VI08,  0(VI14)x
	
BucketTriRefAddr:
	NOP		ISUBIU	VI09, VI09, BucketTriRefAddr
	NOP		IADD	VI15, VI09, VI15

	NOP		ILW.z	VI09,  0(VI14)z
	NOP		IADDIU	VI01, VI00, 0x0004
	NOP		IADDIU	VI01, VI01, 0x7fff
	NOP		ISW.z	VI08,  0(VI14)z
	NOP		ISW.x	VI09,  0(VI14)x
	NOP		ISUBIU	VI09, VI08, 1
	NOP		ISW.x	VI01,  0(VI09)

	NOP		LQ	VF18, -16(VI14)
	NOP		LQ	VF21, -13(VI14)
	NOP		LQ	VF19, -15(VI14)
	NOP		LQ	VF20, -14(VI14)
	MULz.xy	VF18, VF18, VF18	LQ	VF22, -12(VI14)
	
	MULz.xy	VF21, VF21, VF21	SQ	VF19,   1(VI08)
	NOP		LQ	VF19, -10(VI14)
	NOP		SQ	VF20,   2(VI08)

	
	NOP		SQ	VF18,   0(VI08)
	MULz.xy	VF19, VF19, VF19	SQ	VF21,   3(VI08)
	
	NOP		LQ	VF18, -11(VI14)
	NOP	 	LQ	VF20,  -9(VI14)
	NOP		LQ	VF21,  -8(VI14)

	NOP		SQ	VF22,   4(VI08)
	
	NOP		SQ	VF18,   5(VI08)
	NOP		SQ	VF19,   6(VI08)
	NOP		SQ	VF20,   7(VI08)
	NOP		SQ	VF21,   8(VI08)

	NOP		JR	VI15
	NOP		XGKICK	VI09

;-----------------------------------------------------------------------------------------------------------------------------------------------

ClipTri:
	NOP		BAL	VI01, ClipTriRefAddr
	NOP		ISW.x	irZADCPtr, 2(VI14)x		; store off stream ptrs

ClipTriRefAddr:
	NOP		ISUBIU	VI01, VI01, ClipTriRefAddr
	NOP		IADD	VI15, VI15, VI01

	NOP		ISW.y	irVtxPtr,  2(VI14)y

	NOP		ISW.z	irNrmPtr, -3(VI14)z
	NOP		ISW.y	irLightingFunc, -3(VI14)y		

	NOP		ISW.x	irEndPtr, -2(VI14)x		; store off End ptrs
	NOP		ISW.y	irClrPtr, -2(VI14)y
	NOP		ISW.z	irADCBits, -2(VI14)z		; store off ADCBits
	NOP		ISW.w	irClipFlag2, -2(VI14)w		; store off ClipFlag2

	NOP		ISW.x	VI15, -3(VI14)x		; store return addr

	NOP		SQ	VF29, -6(VI14)
	NOP		SQ	VF30, -7(VI14)

	NOP		IOR	VI13, irClipFlag1, irClipFlag2

	NOP		LQ	VF18, -16(VI14)	
	NOP		LQ	VF19, -13(VI14)	
	NOP		LQ	VF20, -10(VI14)	

	NOP		LQ	VF21, -15(VI14)	
	NOP		LQ	VF22, -12(VI14)	
	NOP		LQ	VF23,  -9(VI14)	
	
	NOP		IOR	VI13, VI13, irClipFlag3		; create ORedCLIPBits
	
	ITOF0	VF21, VF21	ILW.x	VI08,  0(VI14)x
	ITOF0	VF22, VF22	ILW.y	VI09,  0(VI14)y
	ITOF0	VF23, VF23	IADDIU	VI01, VI00, 0x00ff

	NOP		SQ	VF18,  0(VI08)	
	NOP		SQ	VF19,  3(VI08)	
	NOP		SQ	VF20,  6(VI08)	

	NOP		SQ	VF21,  1(VI08)	
	NOP		SQ	VF22,  4(VI08)	
	NOP		SQ	VF23,  7(VI08)	

	NOP		SQ	VF24,  2(VI08)
	NOP		SQ	VF25,  5(VI08)
	NOP		SQ	VF26,  8(VI08)

	NOP		ILW.w	VI01, -1(VI14)w		; load ClipTri func( )

	NOP		IADDIU	VI07, VI00, 3		; set initial vtx count
	NOP		JALR	VI15, VI01		; call ClipTri( )
	NOP		IADDIU	VI10, VI08, 9		; set ClipBuffer_End

	NOP		ILW.x	VI01, -3(VI14)x	

	NOP		ILW.x	irZADCPtr, 2(VI14)x		; store off stream ptrs
	NOP		ILW.y	irVtxPtr,  2(VI14)y

	NOP		ILW.x	irEndPtr, -2(VI14)x		; store off End ptrs
	NOP		ILW.y	irClrPtr, -2(VI14)y
	NOP		ILW.z	irADCBits, -2(VI14)z		; store off ADCBits
	NOP		ILW.w	irClipFlag2, -2(VI14)w		; store off ClipFlag2

	NOP		ILW.y	irLightingFunc, -3(VI14)y		
	NOP		ILW.z	irNrmPtr, -3(VI14)z
	
	NOP		LQ	VF29, -6(VI14)
	NOP		JR	VI01		; return
	NOP		LQ	VF30, -7(VI14)


dbVCSC_OutputClippedFan:
	MULx.w	VF27w, VF00w, VF00x	LQ	VF24, 2(VI08)
	NOP		IADDIU	VI04, VI08, 0
	ADDw.z	VF20z, VF00z, VF00w	IADD	VI05, VI07, VI07
	NOP		IADD	VI05, VI05, VI07		; VI05 = (3 * Count)
	MULAw.xyzw	ACC, VF08, VF24w	IADD	VI05, VI04, VI05		; VI05 = VI04 + (3 * Count)
	MADDax.xyzw	ACC, VF05, VF24x	NOP
	MADDAy.xyzw	ACC, VF06, VF24y	LQ	VF23, 5(VI08)
	MADDz.xyzw	VF22, VF07, VF24z	NOP
	NOP		NOP
	NOP		NOP
	MULAw.xyzw	ACC, VF08, VF23w	LQ	VF24, 8(VI08)
	MADDax.xyzw	ACC, VF05, VF23x	DIV	Q, VF00w, VF22w		; Vtx1 perspective divide
	MADDAy.xyzw	ACC, VF06, VF23y	LQ	VF21, 1(VI08)		; load RGBA
	MADDz.xyzw	VF23, VF07, VF23z	LQ.xyw	VF20, 0(VI08)		; load ST
	NOP		NOP
	NOP		NOP
			
	FTOI0	VF26, VF21	WAITQ
	MULq.xyz	VF25, VF20, Q	NOP
	MULq.xyz	VF27, VF22, Q	NOP

dbVCSC_OutFanLoop:	
	ADDx.w	VF27, VF20, VF00	IADDIU	VI04, VI04, 3
	ADDx.xyzw	VF22, VF23, VF00x	DIV	Q, VF00w, VF23w
	MULAw.xyzw	ACC, VF08, VF24w	LQ	VF21,  1(VI04)
	MADDAx.xyzw	ACC, VF05, VF24x	LQ.xyw	VF20,  0(VI04)
	FTOI4.xyzw	VF27, VF27	SQ	VF26, -2(VI04)
	MADDAy.xyzw	ACC, VF06, VF24y	SQ.xyz	VF25, -3(VI04)
	MADDz.xyzw	VF23, VF07, VF24z	LQ	VF24,  8(VI04)
	FTOI0	VF26, VF21	WAITQ
	MULq.xyz	VF25, VF20, Q	IBNE	VI04, VI05, dbVCSC_OutFanLoop
	MULq.xyz	VF27, VF22, Q	SQ	VF27, -1(VI04)		; load next vtx
	
	NOP		NOP		
	NOP		NOP		
	NOP		JR	VI02		; return
	NOP		NOP			; <ds> 

; ---------------------------------------------------------------------------------------------
define( 'myPrefix', 'dbVCSC2AllCore2Clip')

define( 'PL_Setup', myPrefix'_PL_Setup')
define( 'PLight_Entry', myPrefix'_PL_Entry')

	.global	PL_Setup

PL_Setup:
	NOP		LQI	VF01, (VI01++)	; Load PLight1 Position
	NOP		LQI	VF02, (VI01++)	; Load PLight2 Position
	
	NOP		LQI	VF03, (VI01++)	; Load PLight1 Color
	NOP		LQI	VF04, (VI01++)	; Load PLight2 Color

	MULx.w	VF03, VF00, VF00	LQI	VF05, (VI01++)	; Load PLight InvScale
	MULx.w	VF04, VF00, VF00	LQI	VF06, (VI01++)	; Load Ambient Color
	NOP		LQI	VF07, (VI01++)	; Load Blend Data	
	NOP		LOI	255.0	
	NOP		SQ	VF01, offPLightPositions+0(VI00)
	MULi.w	VF06, VF00, I	SQ	VF02, offPLightPositions+1(VI00)

	NOP		SQ	VF03, offPLightClrs+0(VI00)
	NOP		SQ	VF04, offPLightClrs+1(VI00)

	NOP		SQ	VF05, offPLightScale+0(VI00)
	NOP		SQ	VF06, offALightClr+0(VI00)
	
	NOP		BAL	VI02, PL_RefAddr
	NOP		SQ	VF07, offLightBlendData+0(VI00)
	
PL_RefAddr:
	NOP		ISUBIU	VI02, VI02, PL_RefAddr
	NOP		JR	VI15
	NOP		IADDIU	VI02, VI02, PLight_Entry

PLight_Entry:
	ITOF15.xyz	VF27, VF27	LQ	VF18, offPLightPositions+0(VI00)
	NOP		LQ	VF19, offPLightPositions+1(VI00)
	NOP		LQ.xyz	VF20, offPLightScale+0(VI00)
	NOP		NOP
	SUB.xyz	VF18, VF18, VF26	NOP
	SUB.xyz	VF19, VF19, VF26	NOP
	NOP		NOP
	NOP		NOP
	MUL.xyz	VF18, VF18, VF20	NOP
	MUL.xyz	VF19, VF19, VF20	NOP
	NOP		NOP
	NOP		NOP
	ADDw.xyz	VF26, VF00, VF00w	ERLENG	P, VF18
	MUL.xyz	VF20, VF19, VF19	NOP
	MUL.xyz	VF18, VF18, VF27	NOP
	MUL.xyz	VF19, VF19, VF27	NOP
	NOP		NOP
	MULAx.x	ACC, VF26, VF20x	NOP
	MADDAy.x	ACC, VF26, VF20y	LQ	VF27, -5(VI14)
	MADDz.x	VF20, VF26, VF20z	NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		RSQRT	Q, VF00w, VF20x
 	NOP		NOP
	MINIw.w	VF20, VF25, VF27	NOP

	MULAw.xyz	ACC, VF30, VF00w	LQ	VF21, -13(VI14)
	MADDAx.xy	ACC, VF27, VF24x	LQ	VF22, -12(VI14)
	MADDy.xyz	VF24, VF29, VF24y	LQ	VF23, -11(VI14)

	MAXz.w	VF20, VF20, VF27	NOP

	MULAw.xyzw	ACC, VF08, VF25w	SQ	VF21, -16(VI14)
	MADDAx.xyzw	ACC, VF05, VF25x	SQ	VF22, -15(VI14)
	MADDAy.xyzw	ACC, VF06, VF25y	SQ	VF23, -14(VI14)
	MADDz.xyzw	VF25, VF07, VF25z	NOP
	
	NOP		NOP
	NOP		NOP
	NOP		WAITQ
	ADDq.y	VF20, VF00, Q	DIV	Q, VF00w, VF25w
	
	MULAw.w	ACC, VF30, VF00w	WAITP
	MSUBw.w	VF25, VF29, VF20w	MFP.x	VF20, P
	
	NOP		NOP
	NOP		NOP
	NOP		NOP
	MUL.xy	VF26, VF20, VF20	MOVE.w	VF24, VF25
	NOP		NOP
	MULq.xyz	VF25, VF25, Q	LQ	VF21, -10(VI14)
	ADDq.z	VF24, VF00, Q	LQ	VF22,  -9(VI14)
	MUL.xy	VF20, VF26, VF20	LQ	VF23,  -8(VI14)
	NOP		NOP
	NOP		SQ	VF21, -13(VI14)
	FTOI4.xyzw	VF25, VF25	SQ	VF22, -12(VI14)
	MULAx.x	ACC, VF20, VF18x	SQ	VF23, -11(VI14)
	MADDAy.x	ACC, VF20, VF18y	NOP
	MADDz.x	VF20, VF20, VF18z	NOP
	MULAx.y	ACC, VF20, VF19x	SQ	VF24, -10(VI14)
	MADDAy.y	ACC, VF20, VF19y	SQ	VF25,  -8(VI14)
	MADDz.y	VF20, VF20, VF19z	NOP
	
	ITOF0	VF28, VF28	NOP
	NOP		LQ	VF26, offLightBlendData+0(VI00)
	NOP		NOP
	
	MULw.x	VF20, VF20, VF18w	NOP
	MULw.y	VF20, VF20, VF19w	NOP

	MULAx.xyz	ACC, VF28, VF26x	NOP

	MULx.w	VF19, VF00, VF00	NOP

	MULy.xyz	VF28, VF28, VF26y	LQ.xyzw	VF21, offALightClr+0(VI00)

	MAXx.xy	VF20, VF20, VF00x	LQ.xyz	VF18, offPLightClrs+0(VI00)

	ADDAw.w	ACC, VF26, VF28w	LQ.xyz	VF19, offPLightClrs+1(VI00)
	NOP		IADDIU	VI10, VI00, 0x0E
	MADDAw.xyz	ACC, VF21, VF00w	FMAND	VI01, VI10
	MADDAx.xyz	ACC, VF18, VF20x	IBEQ	VI01, VI10, PL_NoVertexMul
	MADDy.xyzw	VF26, VF19, VF20y	NOP
	
	NOP		NOP
	NOP		NOP
	NOP		NOP
	MUL.xyz	VF26, VF26, VF28	NOP

PL_NoVertexMul:
	NOP		NOP
	NOP		NOP
	NOP		NOP
	
	MINIw.xyzw	VF28, VF26, VF21w	NOP
	
	NOP		NOP
	FTOI0	VF28, VF28	NOP
	NOP		JR	VI15
	NOP		SQ	VF28, -9(VI14)


.enddmadata
dbVertexColorSetCompress2AllCore2Clip_CodeEnd:
	

