	.vu
include( `VUDefines.vm4')

 	.global 	dbAnimLF_CodeBegin
	.global	dbAnimLF_CodeEnd
	.global	dbAnimLF_Start

	tableentry(ALFFinish)
	
	tableentry(ALFLoadStreamToCache)
	
	tableentry(ALFLoadCacheToMtx1)
	tableentry(ALFLoadCacheToMtx2)

	tableentry(ALFLoadStreamToMtx1)
	tableentry(ALFLoadStreamToMtx2)
	
	tableentry(ALFLoadNextInst)
	tableentry(ALFTransform_ProcessInst)
	
	tableentry(ALFTransform1_Start)
	tableentry(ALFTransform1_ContinueNW)
	tableentry(ALFTransform1_ContinueNW_ADC)
	tableentry(ALFTransform1_Continue)
	tableentry(ALFTransform1_Continue_ADC)
	tableentry(ALFTransform1_ContinueNR)
	tableentry(ALFTransform1_ContinueNR_ADC)

	tableentry(ALFTransform2_Start)
	tableentry(ALFTransform2_ContinueNW)
	tableentry(ALFTransform2_ContinueNW_ADC)
	tableentry(ALFTransform2_Continue)
	tableentry(ALFTransform2_Continue_ADC)
	tableentry(ALFTransform2_ContinueNR)
	tableentry(ALFTransform2_ContinueNR_ADC)
	
	tableentry(ALFTransform_Finish)
	tableentry(ALFTransform_Finish_ADC)
		
.dmadata dbAnimLF_CodeBegin

ALFStart:
	NOP		BAL	VI09, ALF_RefAddress
	NOP		NOP
ALF_RefAddress:
	NOP		XTOP	VI10	; Grab Buffer Base

ALFContinue:
	NOP		IADDIU	VI12, VI10, 2	; Base GIFData Ptr

; load first header QWord
	NOP		ILW.y	VI03, 0(VI10)y	; DirtyMatrix Count
	NOP		ILW.x	VI02, 0(VI10)x	; MatrixCacheBase Offset
	NOP		ILW.z	VI04, 0(VI10)z	; DirtyMatrixBits1
	NOP		ILW.w	VI05, 0(VI10)w	; DirtyMatrixBits2
	
	NOP		IBEQ	VI03, VI00, AL_LoadHdr2
	NOP		NOP
	
	NOP		BAL	VI14, A_UpdateDirtyMatrices
	NOP		NOP

AL_LoadHdr2:
	NOP		ILW.y	VI04, 1(VI10)y	; InputBuffer Offset
	NOP		ILW.z	VI11, 1(VI10)z	; XGKICK Offset
	NOP		ILW.w	VI02, 1(VI10)w	; Flags | StatePacketSize
	NOP		ILW.x	VI01, 1(VI10)x	; Vertex Count

	NOP		IADDIU	VI15, VI00, 0x00ff	; mask out StatePacketSize
	NOP		IAND	VI02, VI02, VI15

	NOP		IADD	VI04, VI04, VI10	; InGeomBuffer Ptr
	NOP		IADD	VI11, VI11, VI10	; XGKICK Ptr
	NOP		IBEQ	VI02, VI00, ALFPreProcessSetup
	NOP		IADD	VI06, VI11, VI00	; Active OutGIFBuffer Ptr

ALFCopyGIFData:
	NOP		IADD	VI03, VI12, VI02

ALFCopyLoop:	
	NOP		LQI	VF01, (VI12++)
	NOP		NOP
	NOP		IBNE	VI03, VI12, ALFCopyLoop
	NOP		SQI	VF01, (VI06++)

ALFPreProcessSetup:
	NOP		IADDIU	VI01, VI01, 0x7fff
	NOP		IADDIU	VI01, VI01, 0x0001
	NOP		MFIR.x	VF31x, VI01

	NOP		IADDIU	VI08, VI00, 0x7fff
	NOP		IADDIU	VI08, VI08, 0x0001	; VI08 = ADC bit - 0x8000
	NOP		IADDIU	VI07, VI00, 0x00ff	; VI07 = Instruction mask	

	NOP		IADDIU	VI14, VI06, 0	; temporary GIFTag addr
	NOP		B	ALFLoadNextInst
	NOP		SQI	VF31, (VI06++)	; store GIFTag to OutGIFBuffer	

; ----------------------------------------------------------------------------------------------------------
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

ALFLoadNextInst:
	NOP		ILW.x	VI01, 0(VI12)x	; Load InstructionRoutine Offset
	NOP		ILW.y	VI02, 0(VI12)y
	NOP		ILW.z	VI03, 0(VI12)z
	NOP		IADDIU	VI12, VI12, 1
	NOP		JR	VI01	; Branch to InstructionRoutine
	NOP		IADD	VI13, VI02, VI10	; <ds>

; ----------------------------------------------------------------------------------------------------------
; (InstructionRoutine) FinishUp
;
; ----------------------------------------------------------------------------------------------------------
ALFFinish:
	NOP		LQ	VF31, 0(VI14)
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		XGKICK	VI11
	NOP		NOP
	NOP[E]		NOP
	NOP		NOP
	NOP		B	ALFContinue
	NOP		XTOP	VI10	; Grab Buffer Base
; ----------------------------------------------------------------------------------------------------------
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

; ----------------------------------------------------------------------------------------------------------
; Instruction Data
; VI01 	: Instruction Addr
; VI02 	: Data1
; VI03 	: Data2
; VI12 	: Next Instruction
; VI13	: Data1 Buffer Adjusted
; ----------------------------------------------------------------------------------------------------------

; ----------------------------------------------------------------------------------------------------------
; (InstructionRoutine) Load MatrixCache from Matrix Stream
; VI02	: MatrixStream Offset
; VI03	: MatrixCache Offset
; VI13	: MatrixStream Ptr
; ----------------------------------------------------------------------------------------------------------
ALFLoadStreamToCache:
	NOP		LQI	VF31, (VI13++)	; load matrix from IS
	NOP		SQI	VF31, (VI03++)	; <ds>
	NOP		LQI	VF31, (VI13++)	; load matrix from IS
	NOP		SQI	VF31, (VI03++)	; <ds>
	NOP		LQI	VF31, (VI13++)	; load matrix from IS
	NOP		SQI	VF31, (VI03++)	; <ds>
	NOP		LQI	VF31, (VI13++)	; load matrix from IS
	NOP		B	ALFLoadNextInst	; fetch next Instruction
	NOP		SQI	VF31, (VI03++)	; <ds>
	NOP		NOP		; <pad>

; ----------------------------------------------------------------------------------------------------------
; (InstructionRoutine) Load Matrix From InstructionStream or MatrixCache
; VI12	: InstructionStreamPtr
;
; ----------------------------------------------------------------------------------------------------------
ALFLoadStreamToMtx1:
	NOP		IADDIU	VI03, VI13, 0	; replace ptr with IS ptr
ALFLoadCacheToMtx1:
	NOP		LQI	VF01, (VI03++)
	NOP		LQI	VF02, (VI03++)
	NOP		LQI	VF03, (VI03++)
	NOP		LQI	VF04, (VI03++)
	NOP		LQI.xyz	VF05, (VI03++)
	NOP		LQI.xyz	VF06, (VI03++)
	NOP		B	ALFLoadNextInst	; fetch next Instruction
	NOP		LQI.xyz	VF07, (VI03++)	; <ds>
			
ALFLoadStreamToMtx2:
	NOP		IADDIU	VI03, VI13, 0	; replace ptr with IS ptr
ALFLoadCacheToMtx2:
	NOP		LQI	VF08, (VI03++)
	NOP		LQI	VF09, (VI03++)
	NOP		LQI	VF10, (VI03++)
	NOP		LQI	VF11, (VI03++)
	NOP		LQI.xyz	VF12, (VI03++)
	NOP		LQI.xyz	VF13, (VI03++)
	NOP		B	ALFLoadNextInst
	NOP		LQI.xyz	VF14, (VI03++)
	
;---------------------------------------------------------------------------------------------------------------					       
;---------------------------------------------------------------------------------------------------------------					       

;---------------------------------------------------------------------------------------------------------------					       
; The following code is ordered to minimize in-Vertex instruction space requirements.
;---------------------------------------------------------------------------------------------------------------					       

;---------------------------------------------------------------------------------------------------------------					       
; Start the Vertex Pipeline - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform1_Start:
	ADDw.z	VF27, VF00, VF00w	LQ	VF23, 0(VI04)	; load Vertex 1
	ADDz.z	VF30, VF00, VF00	LQ	VF24, 1(VI04)	; load Normal 1
	NOP		LOI	255.0	; load ColorClamp
	MULAw.xyzw	ACC, VF04, VF00w	IADDIU	VI04, VI04, 2	; inc Vertex/Normal pair ptr
	MADDAx.xyzw	ACC, VF01, VF23x	MTIR	VI01, VF23w
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF25, VF03, VF23z	IADDIU	VI02, VI09, ALFTransform1_ContinueNW - ALF_RefAddress

	MULAw.xyz	ACC, VF05, VF23w	IAND	VI01, VI01, VI07
	MADDAz.xyz	ACC, VF06, VF24z	IADD	VI01, VI01, VI02
	MADDw.xyz	VF26, VF07, VF24w	NOP

	MAXw.w	VF26, VF25, VF05	NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP	
	MINIw.w	VF30, VF26, VF06	NOP
	NOP		NOP	
	NOP		LQI	VF23, (VI04++)
	NOP		JR	VI01
	MULw.w	VF30, VF30, VF07	NOP

;---------------------------------------------------------------------------------------------------------------					       
ALFTransform2_Start:
	ADDw.z	VF27, VF00, VF00w	LQ	VF23, 0(VI04)	; load Vertex 1
	ADDz.z	VF30, VF00, VF00	LQ	VF24, 1(VI04)	; load Normal 1
	NOP		LOI	255.0	; load ColorClamp
	MULAw.xyzw	ACC, VF11, VF00w	IADDIU	VI04, VI04, 2	; inc Vertex/Normal pair ptr
	MADDAx.xyzw	ACC, VF08, VF23x	MTIR	VI01, VF23w
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF25, VF10, VF23z	IADDIU	VI02, VI09, ALFTransform1_ContinueNW - ALF_RefAddress

	MULAw.xyz	ACC, VF12, VF23w	IAND	VI01, VI01, VI07
	MADDAz.xyz	ACC, VF13, VF24z	IADD	VI01, VI01, VI02
	MADDw.xyz	VF26, VF14, VF24w	NOP

	MAXw.w	VF26, VF25, VF05	NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP	
	MINIw.w	VF30, VF26, VF06	NOP
	NOP		NOP	
	NOP		LQI	VF23, (VI04++)
	NOP		JR	VI01
	MULw.w	VF30, VF30, VF07	NOP


;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline w/o Write - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform1_ContinueNW:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF02, VF23y	MR32.z	VF24, VF00
	MADDz.xyzw	VF18, VF03, VF23z	NOP

	MAXx.xyz	VF26, VF26, VF00	MTIR	VI01, VF23w
	NOP		ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	NOP
	
	MAXw.w	VF26, VF18, VF05	NOP
	
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	NOP
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_ProcessInst - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF05, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
ALFTransform1_ContinueNW_ADC:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	MR32.z	VF30, VF12
	MADDAy.xyzw	ACC, VF02, VF23y	MR32.z	VF24, VF00
	MADDz.xyzw	VF18, VF03, VF23z	NOP

	MAXx.xyz	VF26, VF26, VF00	MTIR	VI01, VF23w
	NOP		ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	NOP

	MAXw.w	VF26, VF18, VF05	NOP

	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	NOP
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_ProcessInst - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF05, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP


;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline w/o Write - Matrix 2
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform2_ContinueNW:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF09, VF23y	MR32.z	VF24, VF00
	MADDz.xyzw	VF18, VF10, VF23z	NOP

	MAXx.xyz	VF26, VF26, VF00	MTIR	VI01, VF23w
	NOP		ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	NOP

	MAXw.w	VF26, VF18, VF05	NOP

	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	NOP
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_ProcessInst - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF12, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
ALFTransform2_ContinueNW_ADC:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	MR32.z	VF30, VF12
	MADDAy.xyzw	ACC, VF09, VF23y	MR32.z	VF24, VF00
	MADDz.xyzw	VF18, VF10, VF23z	NOP

	MAXx.xyz	VF26, VF26, VF00	MTIR	VI01, VF23w
	NOP		ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	NOP

	MAXw.w	VF26, VF18, VF05	NOP

	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	NOP
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_ProcessInst - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF12, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Transform Loop Process Instruction
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform_ProcessInst:
	NOP		ILW.x	VI01, 0(VI12)x	; Load InstructionRoutine Offset
	NOP		ILW.y	VI02, 0(VI12)y
	NOP		ILW.z	VI03, 0(VI12)z
	NOP		IADDIU	VI12, VI12, 1
	NOP		JR	VI01	; Branch to InstructionRoutine
	NOP		IADD	VI13, VI02, VI10	; <ds>

;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform1_Continue:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF18, VF03, VF23z	SQI	VF27, (VI06++)

	FTOI0	VF28, VF28	NOP
	MAXx.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00
	FTOI4	VF29, VF29	MTIR	VI01, VF23w

	MAXw.w	VF26, VF18, VF05	ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	SQI	VF28, (VI06++)
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	SQI	VF29, (VI06++)
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_ProcessInst - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF05, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
ALFTransform1_Continue_ADC:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	MR32.z	VF30, VF12
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF18, VF03, VF23z	SQI	VF27, (VI06++)

	FTOI0	VF28, VF28	NOP
	MAXx.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00
	FTOI4	VF29, VF29	MTIR	VI01, VF23w

	MAXw.w	VF26, VF18, VF05	ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	SQI	VF28, (VI06++)
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	SQI	VF29, (VI06++)
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_ProcessInst - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF05, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline - Matrix 2
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform2_Continue:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF18, VF10, VF23z	SQI	VF27, (VI06++)

	FTOI0	VF28, VF28	NOP
	MAXx.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00
	FTOI4	VF29, VF29	MTIR	VI01, VF23w

	MAXw.w	VF26, VF18, VF05	ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	SQI	VF28, (VI06++)
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	SQI	VF29, (VI06++)
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_ProcessInst - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF12, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
ALFTransform2_Continue_ADC:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	MR32.z	VF30, VF12
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF18, VF10, VF23z	SQI	VF27, (VI06++)

	FTOI0	VF28, VF28	NOP
	MAXx.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00
	FTOI4	VF29, VF29	MTIR	VI01, VF23w

	MAXw.w	VF26, VF18, VF05	ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	SQI	VF28, (VI06++)
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	SQI	VF29, (VI06++)
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_ProcessInst - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF12, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline w/o Read - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform1_ContinueNR:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF18, VF03, VF23z	SQI	VF27, (VI06++)

	FTOI0	VF28, VF28	NOP
	MAXx.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00
	FTOI4	VF29, VF29	MTIR	VI01, VF23w

	MAXw.w	VF26, VF18, VF05	ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	SQI	VF28, (VI06++)
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	SQI	VF29, (VI06++)
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_Finish - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF05, VF23w	NOP
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
ALFTransform1_ContinueNR_ADC:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	MR32.z	VF30, VF12
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF18, VF03, VF23z	SQI	VF27, (VI06++)

	FTOI0	VF28, VF28	NOP
	MAXx.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00
	FTOI4	VF29, VF29	MTIR	VI01, VF23w

	MAXw.w	VF26, VF18, VF05	ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	SQI	VF28, (VI06++)
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	SQI	VF29, (VI06++)
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_Finish - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF05, VF23w	NOP
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP


;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline w/o Read - Matrix 2
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform2_ContinueNR:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF18, VF10, VF23z	SQI	VF27, (VI06++)

	FTOI0	VF28, VF28	NOP
	MAXx.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00
	FTOI4	VF29, VF29	MTIR	VI01, VF23w

	MAXw.w	VF26, VF18, VF05	ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	SQI	VF28, (VI06++)
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	SQI	VF29, (VI06++)
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_Finish - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF12, VF23w	NOP
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
ALFTransform2_ContinueNR_ADC:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	MR32.z	VF30, VF12
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF18, VF10, VF23z	SQI	VF27, (VI06++)

	FTOI0	VF28, VF28	NOP
	MAXx.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00
	FTOI4	VF29, VF29	MTIR	VI01, VF23w

	MAXw.w	VF26, VF18, VF05	ESUM	P, VF30

	ADDAx.xyzw	ACC, VF19, VF00x	SQI	VF28, (VI06++)
	MADDAx.xyzw	ACC, VF20, VF26x	MOVE.z	VF30, VF00
	MADDAy.xyzw	ACC, VF21, VF26y	SQI	VF29, (VI06++)
	MADDz.xyzw	VF28, VF22, VF26z	IAND	VI01, VI01, VI07

	MINIw.xyzw	VF26, VF26, VF06	IADDIU	VI01, VI01, ALFTransform_Finish - ALF_RefAddress

	MULq.xyz	VF27, VF24, Q	NOP
	MULq.xyz	VF29, VF25, Q	LQI	VF24, (VI04++)

	MINIi.xyzw	VF28, VF28, I	MOVE	VF25, VF18
	MULw.w	VF30, VF26, VF07	IADD	VI01, VI01, VI09

	MULAw.xyz	ACC, VF12, VF23w	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFP.w	VF29w, P
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Finish the Pipeline - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       
ALFTransform_Finish:
	NOP		MOVE.z	VF30, VF00
	FTOI0	VF28, VF28	NOP
	NOP		SQI	VF27, (VI06++)

	MAXx.xyz	VF26, VF26, VF00	DIV	Q, VF00w, VF25w
	FTOI4	VF29, VF29	NOP
	NOP		NOP
	NOP		ESUM	P, VF30

	NOP		NOP
	NOP		SQI	VF28, (VI06++)
	MINIw.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00	; set 1.0

	NOP		SQI	VF29, (VI06++)
	NOP		MOVE	VF29, VF25
	NOP		MOVE.z	VF30, VF00

	ADDAx.xyzw	ACC, VF19, VF00x	ILW.x	VI01, 0(VI12)x
	MADDAx.xyzw	ACC, VF20, VF26x	ILW.y	VI02, 0(VI12)y
	MADDAy.xyzw	ACC, VF21, VF26y	ILW.z	VI03, 0(VI12)z
	MADDz.xyzw	VF28, VF22, VF26z	IADDIU	VI12, VI12, 1

	MULq.xyz	VF29, VF29, Q	WAITP
	MULq.xyz	VF27, VF24, Q	MFP.w	VF29w, P
	NOP		NOP
	MINIi.xyzw	VF28, VF28, I	NOP
	FTOI0	VF28, VF28	NOP
	FTOI4	VF29, VF29	NOP
	NOP		NOP
	NOP		SQI	VF27, (VI06++)
	NOP		SQI	VF28, (VI06++)
	NOP		JR	VI01
	NOP		SQI	VF29, (VI06++)

	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
ALFTransform_Finish_ADC:
	NOP		MR32.z	VF30, VF12
	FTOI0	VF28, VF28	DIV	Q, VF00w, VF25w

	NOP		SQI	VF27, (VI06++)
	MAXx.xyz	VF26, VF26, VF00	NOP
	FTOI4	VF29, VF29	ESUM	P, VF30

	NOP		NOP
	NOP		SQI	VF28, (VI06++)
	MINIw.xyz	VF26, VF26, VF00	MR32.z	VF24, VF00	; set 1.0

	NOP		SQI	VF29, (VI06++)
	NOP		MOVE	VF29, VF25
	NOP		MOVE.z	VF30, VF00

	ADDAx.xyzw	ACC, VF19, VF00x	ILW.x	VI01, 0(VI12)x
	MADDAx.xyzw	ACC, VF20, VF26x	ILW.y	VI02, 0(VI12)y
	MADDAy.xyzw	ACC, VF21, VF26y	ILW.z	VI03, 0(VI12)z
	MADDz.xyzw	VF28, VF22, VF26z	IADDIU	VI12, VI12, 1

	MULq.xyz	VF29, VF29, Q	WAITP
	MULq.xyz	VF27, VF24, Q	MFP.w	VF29w, P
	NOP		NOP
	MINIi.xyzw	VF28, VF28, I	NOP
	FTOI0	VF28, VF28	NOP
	FTOI4	VF29, VF29	NOP
	NOP		NOP
	NOP		SQI	VF27, (VI06++)
	NOP		SQI	VF28, (VI06++)
	NOP		JR	VI01
	NOP		SQI	VF29, (VI06++)


;---------------------------------------------------------------------------------------------------------------					       
; Entry Point - Put at bottom so it doesnt skew fixed addresses
;---------------------------------------------------------------------------------------------------------------					       
dbAnimLF_Start:
	NOP		XTOP	VI01
	NOP		LOI	16.0	; Multiply Zs x 16 for XYZF register
	
	NOP		LQ	VF09, 0(VI01)	; Common->Screen Mtx 1
	NOP		LQ	VF10, 1(VI01)	; Common->Screen Mtx 2
	NOP		LQ	VF11, 2(VI01)	; Common->Screen Mtx 3
	NOP		LQ	VF12, 3(VI01)	; Common->Screen Mtx 4
	
	NOP		LQ	VF05, 4(VI01)	; Obj->Common Mtx 1
	NOP		LQ	VF06, 5(VI01)	; Obj->Common Mtx 2
	NOP		LQ	VF07, 6(VI01)	; Obj->Common Mtx 3
	NOP		LQ	VF08, 7(VI01)	; Obj->Common Mtx 4
	
	MULi.z	VF09, VF09, I	LQ	VF19,11(VI01)	; Light Ambient
	MULi.z	VF10, VF10, I	LQ	VF20, 8(VI01)	; Light Color Mtx 1
	MULi.z	VF11, VF11, I	LQ	VF21, 9(VI01)	; Light Color Mtx 2
	MULi.z	VF12, VF12, I	LQ	VF22,10(VI01)	; Light Color Mtx 3

	NOP		LQ	VF29,12(VI01)	; FogD { FogN, FogF, FogScale, FogConst }

	NOP		LQ	VF31,13(VI01)	; Load GIFTag
	
ALF_ConcatMatrices:
	MULAx.xyzw	ACC, VF09, VF05x	NOP
	MADDAy.xyzw	ACC, VF10, VF05y	NOP
	MADDAz.xyzw	ACC, VF11, VF05z	NOP
	MADDw.xyzw	VF01, VF12, VF05w	NOP

	MULAx.xyzw	ACC, VF09, VF06x	NOP
	MADDAy.xyzw	ACC, VF10, VF06y	NOP
	MADDAz.xyzw	ACC, VF11, VF06z	NOP
	MADDw.xyzw	VF02, VF12, VF06w	NOP

	MULAx.xyzw	ACC, VF09, VF07x	NOP
	MADDAy.xyzw	ACC, VF10, VF07y	NOP
	MADDAz.xyzw	ACC, VF11, VF07z	NOP
	MADDw.xyzw	VF03, VF12, VF07w	NOP
	
	MULAx.xyzw	ACC, VF09, VF08x	NOP
	MADDAy.xyzw	ACC, VF10, VF08y	NOP
	MADDAz.xyzw 	ACC, VF11, VF08z	NOP
	MADDw.xyzw	VF04, VF12, VF08w	NOP

ALF_SetupPersistantData:
	NOP		LOI	2048

	MULI.w	VF12, VF00, I	MR32.w	VF07, VF00

	ADDx.x	VF30, VF00, VF00	NOP
	ADDw.y	VF30, VF00, VF29	NOP

	MULx.w	VF05, VF00, VF29	NOP
	MULy.w	VF06, VF00, VF29	NOP

	SUBz.w	VF07, VF07, VF29	NOP
	
ALF_BranchFirstBuffer:
	NOP		SQ	VF01, 0(VI00)
	NOP		SQ	VF02, 1(VI00)
	NOP		SQ	VF03, 2(VI00)
	NOP[E]		B	ALFStart
	NOP		SQ	VF04, 3(VI00)

;--------------------------------------------------------------------------------------------------
define( 'MatrixSize', eval( 9))

define( 'irTemp1', 'VI01')
define( 'irCurrentBit', 'VI06')

define( 'irMatrixAddr', 'VI02')
define( 'irDirtyMtxCnt', 'VI03')
define( 'irDirtyMtxBits1', 'VI04')
define( 'irDirtyMtxBits2', 'VI05')
define( 'irReturnAddr', 'VI14')

include( 'AnimMatrixCheck.vm4')
;--------------------------------------------------------------------------------------------------
	
.enddmadata
dbAnimLF_CodeEnd:


