.vu

include( `VUDefines.vm4')


define( 'szeDblBuffer', eval( (szeVUMemAvailable) / 2))

define( 'irCopyCount', 'VI01')
define( 'irBaseVtxPtr', 'VI02')
define( 'irKickPtr', 'VI03')
define( 'irEndPtr', 'VI04')

define( 'irOutPtr', 'VI05')
define( 'irVtxPtr', 'VI07')
define( 'irZADCPtr', 'VI08')

define( 'irVtxCount', 'VI10')
define( 'irLoopCount', 'VI11')
define( 'irADCMask', 'VI12')

	.global	dbStaticCompress2VTP_CodeBegin
	.global	dbStaticCompress2VTP_CodeEnd

.dmadata dbStaticCompress2VTP_CodeBegin
	NOP		XTOP	irBaseVtxPtr	; get ptr to VUBuffer_1
	NOP		LOI	16.0	; for scaling z values for XYZF2 register
	
	NOP		LQ	VF09, 0(irBaseVtxPtr)	; Load CommonToScreenMtx row 1
	NOP		LQ	VF10, 1(irBaseVtxPtr)	; Load CommonToScreenMtx row 2
	NOP		LQ	VF11, 2(irBaseVtxPtr)	; Load CommonToScreenMtx row 3
	NOP		LQ	VF12, 3(irBaseVtxPtr)	; Load CommonToScreenMtx row 4
	NOP		LQ	VF06, 5(irBaseVtxPtr)	; Load ObjToCommonMtx row1
	NOP		LQ	VF05, 4(irBaseVtxPtr)	; Load ObjToCommonMtx row1
	NOP		LQ	VF07, 6(irBaseVtxPtr)	; Load ObjToCommonMtx row1
	NOP		LQ	VF08, 7(irBaseVtxPtr)	; Load ObjToCommonMtx row1
	MULi.z	VF09, VF09, I	LQ	VF20, 8(irBaseVtxPtr)	; Load FogData { FogNear, FogFar, FogScale, FogConst }
	MULi.z	VF10, VF10, I	LQ	VF27, 9(irBaseVtxPtr)	; Load TextureMtx1 
	MULi.z	VF11, VF11, I	LQ	VF28,10(irBaseVtxPtr)	; Load TextureMtx2
	MULi.z	VF12, VF12, I	LQ	VF29,11(irBaseVtxPtr)	; Load TextureMtx3
	NOP		LQ	VF30,12(irBaseVtxPtr)	; Load TextureMtx4
	NOP		LQ	VF19,13(irBaseVtxPtr)	; Load ConstantColor { w = alphaMul }
	NOP[E]		LQ	VF31,14(irBaseVtxPtr)	; Load GIFTag
	NOP		NOP

dbSC_ConcatMatrices:
	MULAx.xyzw	ACC, VF09, VF05x	NOP
	MADDAy.xyzw	ACC, VF10, VF05y	NOP
	MADDAz.xyzw	ACC, VF11, VF05z	NOP
	MADDw.xyzw	VF01, VF12, VF05w	NOP

	MULAx.xyzw	ACC, VF09, VF06x	NOP
	MADDAy.xyzw	ACC, VF10, VF06y	NOP
	MADDAz.xyzw	ACC, VF11, VF06z	NOP
	MADDw.xyzw	VF02, VF12, VF06w	NOP

	MULAx.xyzw	ACC, VF09, VF07x	IADDIU	VI01, VI00, 0x7fff
	MADDAy.xyzw	ACC, VF10, VF07y	IADDIU	VI01, VI01, 0x0001
	MADDAz.xyzw	ACC, VF11, VF07z	ISW.x	VI01, 0(VI00)
	MADDw.xyzw	VF03, VF12, VF07w	NOP
	
	MULAx.xyzw	ACC, VF09, VF08x	NOP
	MADDAy.xyzw	ACC, VF10, VF08y	NOP
	MADDAz.xyzw  ACC, VF11, VF08z	NOP
	MADDw.xyzw	VF04, VF12, VF08w	XGKICK	VI00

	NOP[E]		NOP
	NOP		NOP
	
dbSC_Setup:
; set up Const Registers
; Inputs:
; VF19	{ R, G, B, alphaMul }
; VF20	{ FogNear,   FogFar,    FogScale,  FogConst  }
; VF27           { TexMtx1.x, TexMtx1.y, TexMtx1.z, TexMtx1.w }
; VF28           { TexMtx2.x, TexMtx2.y, TexMtx2.z, TexMtx2.w }
; VF29           { TexMtx3.x, TexMtx3.y, TexMtx3.z, TexMtx3.w }
; VF30           { TexMtx4.x, TexMtx4.y, TexMtx4.z, TexMtx4.w }

; Outputs:
; VF18	{ 0.0,       FogConst,  ??,       ??        }
; VF25	{ ADCMul1,   ADCMul2,   ADCMul3,  ADCMul4   }
; VF26	{ FogNear,   FogFar,   -FogScale, alphaMul  }
; VF27	{ TexMtx1.x, TexMtx1.y, ??        TexMtx1.w }
; VF28	{ TexMtx2.x, TexMtx2.y, ??        TexMtx2.w }
; VF29	{ TexMtx3.x, TexMtx3.y, 0.0,      TexMtx3.w }
; VF30	{ TexMtx4.x, TexMtx4.y, 1.0,      TexMtx4.w }

	ADDx.x	VF18, VF00, VF00	MR32.w	VF21, VF00	; VF18	{ 0.0, ??, ??, ?? }
					; temp Reg VF01.w = 0.0f 
	ADDw.y	VF18, VF00, VF20	MOVE.z	VF29, VF00	; VF18    { 0.0, FogConst, ??, ?? }
					; VF29    { TexMtx3.x  TexMtx3.y  0.0,      TexMtx3.w }
	ADDw.z	VF30, VF00, VF00	LOI	2048	; VF30	{ TexMtx4.x, TexMtx4.y, 1.0,      TexMtx4.w }
	ADDI.x	VF25, VF00, I	LOI	1024	; VF25	{ ADCMul1,   ??,        ??,       ??        }
	ADDI.y	VF25, VF00, I	LOI	512	; VF25	{ ADCMul1,   ADCMul2,   ??,       ??        }
	ADDI.z	VF25, VF00, I	LOI	256	; VF25	{ ADCMul1,   ADCMul2,   ADCMul3,  ??        }
	MULI.w	VF25, VF00, I	MOVE.xy	VF26, VF20	; VF25	{ ADCMul1,   ADCMul2,   ADCMul3,  ADCMul4   }
					; VF26    { FogNear,   FogFar,    ??,       ??        }
	SUBz.z	VF26, VF00, VF20	NOP		; VF26    { FogNear,   FogFar,   -FogScale, ??        }
	NOP		MOVE.w	VF26, VF19	; VF26    { FogNear,   FogFar,   -FogScale, alphaMul  }
	
dbSC_Start:
;	NOP		MOVE.xyw	VF27, VF01
;	NOP		MOVE.xyw	VF28, VF02
;	NOP		MOVE.xyw	VF29, VF03
;	NOP		MOVE.xyw	VF30, VF04
	
	NOP		XTOP	irBaseVtxPtr		; get ptr to VUBuffer

	NOP		ILW.w	irKickPtr, 1(irBaseVtxPtr)w		; grab offsetOutput
	
	NOP		ILW.z	irCopyCount, 0(irBaseVtxPtr)z		; grab copyCount
	NOP		ILW.x	irVtxCount, 0(irBaseVtxPtr)x		; grab vertexCount
	NOP		ILW.y	irLoopCount, 0(irBaseVtxPtr)y		; grab loopCount

	NOP	 	IADD	irKickPtr, irBaseVtxPtr, irKickPtr		; calculate OutBuffer base ptr
	NOP		IADDIU	irBaseVtxPtr, irBaseVtxPtr, 2		; skip LocalHdr
	
	NOP		IADD	irBaseVtxPtr, irBaseVtxPtr, irCopyCount		; skil LocalGIFData
	
	NOP		IADDIU	irOutPtr, irKickPtr, 0		; <ds> calculate OutBuffer moving ptr

define( 'irTemp', 'VI01')
define( 'irADCBits', 'VI06')
define( 'irAlphaWork', 'VI01')
define( 'irAlphaMask', 'VI13')

dbSC_Prolog:
	NOP		IADD	irZADCPtr, irBaseVtxPtr, irVtxCount
	NOP		IADDIU	irVtxPtr, irBaseVtxPtr, 0
	NOP		LQI	VF09, (irVtxPtr++)
	NOP		LQI	VF13, (irZADCPtr++)
	NOP		IADDIU	irAlphaMask, VI00, 0x00ff
	NOP		IADDIU	irADCMask, VI00, 0x000f
	NOP		IADDIU	VI15, irVtxCount, 0x7fff
	NOP		IADDIU	VI15, VI15, 0x0001
	NOP		MFIR.x	VF31, VI15
	NOP		SQI	VF31, (irOutPtr++)
			
	NOP		MTIR	irADCBits, VF13x
	NOP		IAND	irADCBits, irADCBits, irADCMask
	MULAw.xyzw	ACC, VF04, VF00w	NOP
	MADDAz.xyzw	ACC, VF01, VF09z	MTIR	irAlphaWork, VF09w
	NOP		NOP
	NOP		MFIR.z	VF20, irADCBits
	MADDAw.xyzw	ACC, VF02, VF09w	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MADDx.xyzw	VF14, VF03, VF013x	NOP
	MULAx.xyzw	ACC, VF29, VF13x	NOP
	MADDAz.xyw	ACC, VF27, VF09z	NOP
	MADDAw.xyw	ACC, VF28, VF09w	NOP
	ITOF0.z	VF20, VF20	LQI	VF10, (irVtxPtr++)
	MINIy.w	VF20, VF14, VF26y	DIV	Q, VF00w, VF14w
	MADDw.xyzw	VF09, VF30, VF00w	MFIR.w	VF23, irAlphaWork
	MAXx.w	VF20, VF20, VF26x	NOP
	MULAw.xyzw	ACC, VF04, VF00w	NOP
	MADDAz.xyzw	ACC, VF01, VF10z	MTIR	irAlphaWork, VF10w
	NOP		NOP
	NOP		NOP
	MULq.xyz	VF14, VF14, Q	DIV	Q, VF00w, VF09w
	MULq.xyz	VF09, VF09, Q	NOP
	MULz.w	VF18, VF20, VF26z	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MULx.z	VF18, VF20, VF25x	NOP
	MADDAw.xyzw	ACC, VF02, VF10w	MFIR.x	VF23, irAlphaWork
	MADDy.xyzw	VF15, VF03, VF13y	NOP
	MULAy.xyzw	ACC, VF29, VF13y	NOP
	MADDAz.xyw	ACC, VF27, VF10z	ESUM	P, VF18
	MADDAw.xyw	ACC, VF28, VF10w	NOP
	MINIy.w	VF20, VF15, VF26y	DIV	Q, VF00w, VF15w
	NOP		NOP
	MULq.xy	VF09, VF09, Q	LQI	VF11, (irVtxPtr++)
	MADDw.xyzw	VF10, VF30, VF00w	NOP
	MAXx.w	VF20, VF20, VF26x	NOP
	MULAw.xyzw	ACC, VF04, VF00w	NOP
	MADDAz.xyzw	ACC, VF01, VF11z	MTIR	irAlphaWork, VF11w
	MULq.xyz	VF15, VF15, Q	DIV	Q, VF00w, VF10w
	MULq.xyz	VF10, VF10, Q	NOP
	MULz.w	VF18, VF20, VF26z	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MULy.z	VF18, VF20, VF25y	NOP
	MADDAw.xyzw	ACC, VF02, VF11w	MFIR.y	VF23, irAlphaWork
	MADDz.xyzw	VF16, VF03, VF13z	MFP.w	VF14, P
	MULAz.xyzw	ACC, VF29, VF13z	NOP
	MADDAz.xyw	ACC, VF27, VF11z	ESUM	P, VF18
	MADDAw.xyw	ACC, VF28, VF11w	SQ	VF09,  0(irOutPtr)
	MINIy.w	VF20, VF16, VF26y	DIV	Q, VF00w, VF16w
	FTOI4.xyzw	VF14, VF14	NOP
	MULq.xy	VF10, VF10, Q	LQI	VF12, (irVtxPtr++)
	MADDw.xyzw	VF11, VF30, VF00w	NOP
	MAXx.w	VF20, VF20, VF26x	NOP
	MULAw.xyzw	ACC, VF04, VF00w	SQ	VF14,  2(irOutPtr)
	MADDAz.xyzw	ACC, VF01, VF12z	MTIR	irAlphaWork, VF12w
	MULq.xyz	VF16, VF16, Q	DIV	Q, VF00w, VF11w
	MULq.xyz	VF11, VF11, Q	NOP
	MULz.w	VF18, VF20, VF26z	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MULz.z	VF18, VF20, VF25z	NOP
	MADDAw.xyzw	ACC, VF02, VF12w	MFIR.z	VF23, irAlphaWork
	MADDw.xyzw	VF17, VF03, VF13w	MFP.w	VF15, P
	MULAw.xyzw	ACC, VF29, VF13w	NOP
	MADDAz.xyw	ACC, VF27, VF12z	ESUM	P, VF18
	MADDAw.xyw	ACC, VF28, VF12w	SQ	VF10,  3(irOutPtr)
	ITOF0	VF23, VF23	LQI	VF13, (irZADCPtr++)
	MINIy.w	VF20, VF17, VF26y	DIV	Q, VF00w, VF17w
	FTOI4.xyzw	VF15, VF15	NOP
	MULq.xy	VF11, VF11, Q	LQI	VF09, (irVtxPtr++)
	MULw.xyzw	VF24, VF23, VF26w	NOP
	MADDw.xyzw	VF12, VF30, VF00w	MTIR	irADCBits, VF13x
	MAXx.w	VF20, VF20, VF26x	IAND	irADCBits, irADCBits, irADCMask
	MULAw.xyzw	ACC, VF04, VF00w	SQ	VF15,  5(irOutPtr)
	MADDAz.xyzw	ACC, VF01, VF09z	MTIR	irAlphaWork, VF09w
	FTOI0.xyzw	VF24, VF24	ISUBIU	irLoopCount, irLoopCount, 1
	MULz.w	VF18, VF20, VF26z	DIV	Q, VF00w, VF12w
	MULw.z	VF18, VF20, VF25w	NOP
	MULq.xyz	VF17, VF17, Q	MFIR.z	VF20, irADCBits
	MULq.xyz	VF12, VF12, Q	NOP
	MADDAw.xyzw	ACC, VF02, VF09w	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MADDx.xyzw	VF14, VF03, VF13x	MFP.w	VF16, P
	MULAx.xyzw	ACC, VF29, VF13x	MOVE.w	VF19, VF24w
	MADDAz.xyw	ACC, VF27, VF09z	ESUM	P, VF18
	MADDAw.xyw	ACC, VF28, VF09w	SQ	VF11,  6(irOutPtr)
	ITOF0.z	VF20, VF20	NOP
	MINIy.w	VF20, VF14, VF26y	DIV	Q, VF00w, VF14w

dbSC_Loop:
	FTOI4.xyzw	VF16, VF16	SQ	VF19,  1(irOutPtr)
	MULq.xy	VF12, VF12, Q	LQI	VF10, (irVtxPtr++)
	MADDw.xyzw	VF09, VF30, VF00w	MFIR.w	VF23, irAlphaWork
	MAXx.w	VF20, VF20, VF26x	MR32.w	VF19, VF24
	MULAw.xyzw	ACC, VF04, VF00w	SQ	VF16,  8(irOutPtr)
	MADDAz.xyzw	ACC, VF01, VF10z	MTIR	irAlphaWork, VF10w
	MULq.xyz	VF14, VF14, Q	DIV	Q, VF00w, VF09w
	MULq.xyz	VF09, VF09, Q	NOP
	MULz.w	VF18, VF20, VF26z	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MULx.z	VF18, VF20, VF25x	NOP
	MADDAw.xyzw	ACC, VF02, VF10w	MFIR.x	VF23, irAlphaWork
	MADDy.xyzw	VF15, VF03, VF13y	MFP.w	VF17w, P
	MULAy.xyzw	ACC, VF29, VF13y	MR32.xy	VF24, VF24
	MADDAz.xyw	ACC, VF27, VF10z	ESUM	P, VF18
	MADDAw.xyw	ACC, VF28, VF10w	SQ	VF12,  9(irOutPtr)
	MINIy.w	VF20, VF15, VF26y	DIV	Q, VF00w, VF15w
	FTOI4.xyzw	VF17, VF17	MR32.xw	VF24, VF24
	MULq.xy	VF09, VF09, Q	LQI	VF11, (irVtxPtr++)
	MADDw.xyzw	VF10, VF30, VF00w	SQ	VF19,  4(irOutPtr)
	MAXx.w	VF20, VF20, VF26x	NOP
	MULAw.xyzw	ACC, VF04, VF00w	MOVE.w	VF19, VF24w
	MADDAz.xyzw	ACC, VF01, VF11z	MTIR	irAlphaWork, VF11w
	MULq.xyz	VF15, VF15, Q	DIV	Q, VF00w, VF10w
	MULq.xyz	VF10, VF10, Q	NOP
	MULz.w	VF18, VF20, VF26z	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MULy.z	VF18, VF20, VF25y	SQ	VF17, 11(irOutPtr)
	MADDAw.xyzw	ACC, VF02, VF11w	MFIR.y	VF23, irAlphaWork
	MADDz.xyzw	VF16, VF03, VF13z	MFP.w	VF14, P
	MULAz.xyzw	ACC, VF29, VF13z	NOP
	MADDAz.xyw	ACC, VF27, VF11z	ESUM	P, VF18
	MADDAw.xyw	ACC, VF28, VF11w	SQ	VF09, 12(irOutPtr)
	MINIy.w	VF20, VF16, VF26y	DIV	Q, VF00w, VF16w
	FTOI4.xyzw	VF14, VF14	SQ	VF19,  7(irOutPtr)
	MULq.xy	VF10, VF10, Q	LQI	VF12, (irVtxPtr++)
	MADDw.xyzw	VF11, VF30, VF00w	MR32.w	VF19, VF24
	MAXx.w	VF20, VF20, VF26x	NOP
	MULAw.xyzw	ACC, VF04, VF00w	SQ	VF14, 14(irOutPtr)
	MADDAz.xyzw	ACC, VF01, VF12z	MTIR	irAlphaWork, VF12w
	MULq.xyz	VF16, VF16, Q	DIV	Q, VF00w, VF11w
	MULq.xyz	VF11, VF11, Q	NOP
	MULz.w	VF18, VF20, VF26z	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MULz.z	VF18, VF20, VF25z	NOP
	MADDAw.xyzw	ACC, VF02, VF12w	MFIR.z	VF23, irAlphaWork
	MADDw.xyzw	VF17, VF03, VF13w	MFP.w	VF15, P
	MULAw.xyzw	ACC, VF29, VF13w	NOP
	MADDAz.xyw	ACC, VF27, VF12z	ESUM	P, VF18
	MADDAw.xyw	ACC, VF28, VF12w	SQ	VF10, 15(irOutPtr)
	ITOF0	VF23, VF23	LQI	VF13, (irZADCPtr++)
	MINIy.w	VF20, VF17, VF26y	DIV	Q, VF00w, VF17w
	FTOI4.xyzw	VF15, VF15	SQ	VF19, 10(irOutPtr)
	MULq.xy	VF11, VF11, Q	LQI	VF09, (irVtxPtr++)
	MULw.xyzw	VF24, VF23, VF26w	NOP
	MADDw.xyzw	VF12, VF30, VF00w	MTIR	irADCBits, VF13x
	MAXx.w	VF20, VF20, VF26x	IAND	irADCBits, irADCBits, irADCMask
	MULAw.xyzw	ACC, VF04, VF00w	SQ	VF15, 17(irOutPtr)
	MADDAz.xyzw	ACC, VF01, VF09z	MTIR	irAlphaWork, VF09w
	FTOI0.xyzw	VF24, VF24	ISUBIU	irLoopCount, irLoopCount, 1
	MULz.w	VF18, VF20, VF26z	DIV	Q, VF00w, VF12w
	MULw.z	VF18, VF20, VF25w	IADDIU	irOutPtr, irOutPtr, 12
	MULq.xyz	VF17, VF17, Q	MFIR.z	VF20, irADCBits
	MULq.xyz	VF12, VF12, Q	NOP
	MADDAw.xyzw	ACC, VF02, VF09w	IAND	irAlphaWork, irAlphaWork, irAlphaMask
	MADDx.xyzw	VF14, VF03, VF13x	MFP.w	VF16, P
	MULAx.xyzw	ACC, VF29, VF13x	MOVE.w	VF19, VF24w
	MADDAz.xyw	ACC, VF27, VF09z	ESUM	P, VF18
	MADDAw.xyw	ACC, VF28, VF09w	SQ	VF11,  6(irOutPtr)
	ITOF0.z	VF20, VF20	IBNE	irLoopCount, VI00, dbSC_Loop
	MINIy.w	VF20, VF14, VF26y	DIV	Q, VF00w, VF14w



dbSC_Epilog:
	FTOI4.xyzw	VF16, VF16	SQ	VF19,  1(irOutPtr)
	MULq.xyz	VF12, VF12, Q	MR32.w	VF19, VF24
	NOP		MR32.xy	VF24, VF24
	NOP		NOP
	NOP		SQ	VF16,  8(irOutPtr)
	NOP		SQ	VF12,  9(irOutPtr)
	NOP		MR32.xw	VF24, VF24
	NOP		MFP.w	VF17w, P
	NOP		SQ	VF19,  4(irOutPtr)
	NOP		NOP
	NOP		MOVE.w	VF19, VF24
	FTOI4.xyzw	VF17, VF17	MR32.w	VF24, VF24
	NOP		MOVE.xyz	VF24, VF19
	NOP		NOP
	NOP		NOP
	NOP		SQ	VF19,  7(irOutPtr)
	NOP		SQ	VF24, 10(irOutPtr)
	NOP		SQ	VF17, 11(irOutPtr)

dbSC_Kick:

	NOP		NOP
	NOP		NOP

	NOP		XGKICK	irKickPtr
	NOP		NOP
	NOP[E]		NOP
	NOP		NOP
	
	NOP		B	dbSC_Start
	NOP		NOP
	NOP		NOP

.enddmadata
dbStaticCompress2VTP_CodeEnd:

