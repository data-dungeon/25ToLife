	.vu
include( `VUDefines.vm4')

 	.global 	dbAnimLC_CodeBegin
	.global	dbAnimLC_CodeEnd

	.global	dbAnimLC_Start

	tableentry(ALCFinish)
	
	tableentry(ALCLoadStreamToCache)
	
	tableentry(ALCLoadCacheToMtx1)
	tableentry(ALCLoadCacheToMtx2)

	tableentry(ALCLoadStreamToMtx1)
	tableentry(ALCLoadStreamToMtx2)
	
	tableentry(ALCLoadNextInst)
	tableentry(ALCTransform_ProcessInst)
	
	tableentry(ALCTransform1_Start)
	tableentry(ALCTransform1_ContinueNW)
	tableentry(ALCTransform1_ContinueNW_ADC)
	tableentry(ALCTransform1_Continue)
	tableentry(ALCTransform1_Continue_ADC)
	tableentry(ALCTransform1_ContinueNR)
	tableentry(ALCTransform1_ContinueNR_ADC)

	tableentry(ALCTransform2_Start)
	tableentry(ALCTransform2_ContinueNW)
	tableentry(ALCTransform2_ContinueNW_ADC)
	tableentry(ALCTransform2_Continue)
	tableentry(ALCTransform2_Continue_ADC)
	tableentry(ALCTransform2_ContinueNR)
	tableentry(ALCTransform2_ContinueNR_ADC)
	
	tableentry(ALCTransform_Finish)
	tableentry(ALCTransform_Finish_ADC)
		
.dmadata dbAnimLC_CodeBegin

ALCStart:
	NOP		BAL	VI09, ALC_RefAddress
	NOP		NOP
ALC_RefAddress:
	NOP		XTOP	VI10	; Grab Buffer Base

ALCContinue:
	NOP		IADDIU	VI12, VI10, 2	; Base GIFData Ptr

; load first header QWord
	NOP		ILW.y	VI03, 0(VI10)y	; DirtyMatrix Count
	NOP		ILW.x	VI02, 0(VI10)x	; MatrixCacheBase Offset
	NOP		ILW.z	VI04, 0(VI10)z	; DirtyMatrixBits1
	NOP		ILW.w	VI05, 0(VI10)w	; DirtyMatrixBits2
	
	NOP		IBEQ	VI03, VI00, ALC_LoadHdr2
	NOP		NOP
	
	NOP		BAL	VI14, A_UpdateDirtyMatrices
	NOP		NOP
	
ALC_LoadHdr2:
; load second header QWord
	NOP		ILW.y	VI04, 1(VI10)y	; InputBuffer Offset
	NOP		ILW.z	VI11, 1(VI10)z	; XGKICK Offset
	NOP		ILW.w	VI02, 1(VI10)w	; Flags | StatePacketSize
	NOP		ILW.x	VI01, 1(VI10)x	; Vertex Count

	NOP		IADDIU	VI15, VI00, 0x00ff	; mask out StatePacketSize
	NOP		IAND	VI02, VI02, VI15

	NOP		IADD	VI04, VI04, VI10	; InGeomBuffer Ptr
	NOP		IADD	VI11, VI11, VI10	; XGKICK Ptr
	NOP		IBEQ	VI02, VI00, ALCPreProcessSetup
	NOP		IADD	VI06, VI11, VI00	; Active OutGIFBuffer Ptr

ALCCopyGIFData:
	NOP		IADD	VI03, VI12, VI02

ALCCopyLoop:	
	NOP		LQI	VF01, (VI12++)
	NOP		NOP
	NOP		IBNE	VI03, VI12, ALCCopyLoop
	NOP		SQI	VF01, (VI06++)

ALCPreProcessSetup:
	NOP		IADDIU	VI01, VI01, 0x7fff
	NOP		IADDIU	VI01, VI01, 0x0001
	NOP		MFIR.x	VF31x, VI01

	NOP		IADDIU	VI08, VI00, 0x7fff
	NOP		IADDIU	VI08, VI08, 0x0001	; VI08 = ADC bit - 0x8000
	NOP		IADDIU	VI07, VI00, 0x00ff	; VI07 = Instruction mask	

	NOP		IADDIU	VI14, VI06, 0	; temporary GIFTag addr
	NOP		B	ALCLoadNextInst
	NOP		SQI	VF31, (VI06++)	; store GIFTag to OutGIFBuffer	

; ----------------------------------------------------------------------------------------------------------
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

ALCLoadNextInst:
	NOP		ILW.x	VI01, 0(VI12)x	; Load InstructionRoutine Offset
	NOP		ILW.y	VI02, 0(VI12)y
	NOP		ILW.z	VI03, 0(VI12)z
	NOP		IADDIU	VI12, VI12, 1
	NOP		JR	VI01	; Branch to InstructionRoutine
	NOP		IADD	VI13, VI02, VI10	; <ds>

; ----------------------------------------------------------------------------------------------------------
; (InstructionRoutine) FinishUp
;
; ----------------------------------------------------------------------------------------------------------
ALCFinish:
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		XGKICK	VI11
	NOP		NOP
	NOP[E]		NOP
	NOP		NOP
	NOP		B	ALCContinue
	NOP		XTOP	VI10	; Grab Buffer Base
	NOP		NOP

; ----------------------------------------------------------------------------------------------------------
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

; ----------------------------------------------------------------------------------------------------------
; Instruction Data
; VI01 	: Instruction Addr
; VI02 	: Data1
; VI03 	: Data2
; VI12 	: Next Instruction
; VI13	: Data1 Buffer Adjusted
; ----------------------------------------------------------------------------------------------------------

; ----------------------------------------------------------------------------------------------------------
; (InstructionRoutine) Load MatrixCache from Matrix Stream
; VI02	: MatrixStream Offset
; VI03	: MatrixCache Offset
; VI13	: MatrixStream Ptr
; ----------------------------------------------------------------------------------------------------------
ALCLoadStreamToCache:
	NOP		LQI	VF30, (VI13++)	; load matrix from IS
	NOP		SQI	VF30, (VI03++)	; <ds>
	NOP		LQI	VF30, (VI13++)	; load matrix from IS
	NOP		SQI	VF30, (VI03++)	; <ds>
	NOP		LQI	VF30, (VI13++)	; load matrix from IS
	NOP		SQI	VF30, (VI03++)	; <ds>
	NOP		LQI	VF30, (VI13++)	; load matrix from IS
	NOP		B	ALCLoadNextInst	; fetch next Instruction
	NOP		SQI	VF30, (VI03++)	; <ds>
	NOP		NOP		; <pad>

; ----------------------------------------------------------------------------------------------------------
; (InstructionRoutine) Load Matrix From InstructionStream or MatrixCache
; VI12	: InstructionStreamPtr
;
; ----------------------------------------------------------------------------------------------------------
ALCLoadStreamToMtx1:
	NOP		IADDIU	VI03, VI13, 0	; replace ptr with IS ptr
ALCLoadCacheToMtx1:
	NOP		LQI	VF01, (VI03++)
	NOP		LQI	VF02, (VI03++)
	NOP		LQI	VF03, (VI03++)
	NOP		LQI	VF04, (VI03++)
	NOP		LQI.xyz	VF05, (VI03++)
	NOP		LQI.xyz	VF06, (VI03++)
	NOP		B	ALCLoadNextInst	; fetch next Instruction
	NOP		LQI.xyz	VF07, (VI03++)	; <ds>
			
ALCLoadStreamToMtx2:
	NOP		IADDIU	VI03, VI13, 0	; replace ptr with IS ptr
ALCLoadCacheToMtx2:
	NOP		LQI	VF08, (VI03++)
	NOP		LQI	VF09, (VI03++)
	NOP		LQI	VF10, (VI03++)
	NOP		LQI	VF11, (VI03++)
	NOP		LQI.xyz	VF12, (VI03++)
	NOP		LQI.xyz	VF13, (VI03++)
	NOP		B	ALCLoadNextInst
	NOP		LQI.xyz	VF14, (VI03++)
	
;---------------------------------------------------------------------------------------------------------------					       
;---------------------------------------------------------------------------------------------------------------					       

;---------------------------------------------------------------------------------------------------------------					       
; The following code is ordered to minimize in-Vertex instruction space requirements.
;---------------------------------------------------------------------------------------------------------------					       

;---------------------------------------------------------------------------------------------------------------					       
; Start the Vertex Pipeline - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       
ALCTransform1_Start:
	ADDw.z	VF27, VF00, VF00w	LQ	VF23, 0(VI04)	; load Vertex 1
	NOP		LQ	VF24, 1(VI04)	; load Normal 1
	NOP		LOI	255.0	; load ColorClamp
	MULAw.xyzw	ACC, VF04, VF00w	IADDIU	VI04, VI04, 2	; inc Vertex/Normal pair ptr
	MADDAx.xyzw	ACC, VF01, VF23x	MTIR	VI01, VF23w
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF25, VF03, VF23z	IADDIU	VI02, VI09, ALCTransform1_ContinueNW - ALC_RefAddress

	MULAw.xyz	ACC, VF05, VF23w	IAND	VI01, VI01, VI07
	MADDAz.xyz	ACC, VF06, VF24z	IADD	VI01, VI01, VI02
	MADDw.xyz	VF26, VF07, VF24w	NOP

	CLIPw.xyz	VF25, VF25	NOP
	
	MULAx.xyzw	ACC, VF15, VF25x	NOP
	MADDAy.xyzw	ACC, VF16, VF25y	LQI	VF23, (VI04++)
	MADDAz.xyzw	ACC, VF17, VF25z	JR	VI01
	MADDw.xyzw	VF25, VF18, VF25w	NOP
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
ALCTransform2_Start:
	ADDw.z	VF27, VF00, VF00w	LQ	VF23, 0(VI04)	; load Vertex 1
	NOP		LQ	VF24, 1(VI04)	; load Normal 1
	NOP		LOI	255.0	; load ColorClamp
	MULAw.xyzw	ACC, VF11, VF00w	IADDIU	VI04, VI04, 2	; inc Vertex/Normal pair ptr
	MADDAx.xyzw	ACC, VF08, VF23x	MTIR	VI01, VF23w
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF25, VF10, VF23z	IADDIU	VI02, VI09, ALCTransform1_ContinueNW - ALC_RefAddress

	MULAw.xyz	ACC, VF12, VF23w	IAND	VI01, VI01, VI07
	MADDAz.xyz	ACC, VF13, VF24z	IADD	VI01, VI01, VI02
	MADDw.xyz	VF26, VF14, VF24w	NOP

	CLIPw.xyz	VF25, VF25	NOP
	
	MULAx.xyzw	ACC, VF15, VF25x	NOP
	MADDAy.xyzw	ACC, VF16, VF25y	LQI	VF23, (VI04++)
	MADDAz.xyzw	ACC, VF17, VF25z	JR	VI01
	MADDw.xyzw	VF25, VF18, VF25w	NOP
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline w/o Write - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       
ALCTransform1_ContinueNW:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	FCAND	VI01, 0x3ffff
	MADDAy.xyzw	ACC, VF02, VF23y	IADDIU	VI02, VI01, 0x7fff
	MADDz.xyzw	VF30, VF03, VF23z	NOP

	NOP		MTIR	VI01, VF23w
	NOP		IAND	VI01, VI01, VI07
	MAXx.xyz	VF26, VF26, VF00	IADDIU	VI01, VI01, ALCTransform_ProcessInst - ALC_RefAddress

	CLIPw.xyz	VF30, VF30	IADD	VI01, VI01, VI09

	MULAx.xyzw	ACC, VF15, VF30x	MR32.z	VF24, VF00
	MADDAy.xyzw	ACC, VF16, VF30y	MOVE	VF29, VF25	; copy xfrmed XYZ
	MADDAz.xyzw	ACC, VF17, VF30z	NOP
	MADDw.xyzw	VF25, VF18, VF30w	NOP

	ADDAx.xyzw	ACC, VF19, VF00x	NOP
	MADDAx.xyzw	ACC, VF20, VF26x	NOP
	MADDAy.xyzw	ACC, VF21, VF26y	NOP
	MADDz.xyzw	VF28, VF22, VF26z	NOP

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF05, VF23w	NOP
	MINIi.xyzw	VF28, VF28, I	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFIR.w	VF29w, VI02
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       

ALCTransform1_ContinueNW_ADC:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	NOP
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF30, VF03, VF23z	NOP

	NOP		MTIR	VI01, VF23w
	NOP		IAND	VI01, VI01, VI07
	MAXx.xyz	VF26, VF26, VF00	IADDIU	VI01, VI01, ALCTransform_ProcessInst - ALC_RefAddress

	CLIPw.xyz	VF30, VF30	IADD	VI01, VI01, VI09

	MULAx.xyzw	ACC, VF15, VF30x	MR32.z	VF24, VF00	; set 1.0
	MADDAy.xyzw	ACC, VF16, VF30y	MOVE	VF29, VF25	; copy xfrmed XYZ
	MADDAz.xyzw	ACC, VF17, VF30z	NOP
	MADDw.xyzw	VF25, VF18, VF30w	NOP

	ADDAx.xyzw	ACC, VF19, VF00x	NOP
	MADDAx.xyzw	ACC, VF20, VF26x	NOP
	MADDAy.xyzw	ACC, VF21, VF26y	NOP
	MADDz.xyzw	VF28, VF22, VF26z	NOP

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF05, VF23w	NOP
	MINIi.xyzw	VF28, VF28, I	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFIR.w	VF29w, VI08
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline w/o Write - Matrix 2
;---------------------------------------------------------------------------------------------------------------					       

ALCTransform2_ContinueNW:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	FCAND	VI01, 0x3ffff
	MADDAy.xyzw	ACC, VF09, VF23y	IADDIU	VI02, VI01, 0x7fff
	MADDz.xyzw	VF30, VF10, VF23z	NOP

	NOP		MTIR	VI01, VF23w
	NOP		IAND	VI01, VI01, VI07
	MAXx.xyz	VF26, VF26, VF00	IADDIU	VI01, VI01, ALCTransform_ProcessInst - ALC_RefAddress

	CLIPw.xyz	VF30, VF30	IADD	VI01, VI01, VI09

	MULAx.xyzw	ACC, VF15, VF30x	MR32.z	VF24, VF00
	MADDAy.xyzw	ACC, VF16, VF30y	MOVE	VF29, VF25	; copy xfrmed XYZ
	MADDAz.xyzw	ACC, VF17, VF30z	NOP
	MADDw.xyzw	VF25, VF18, VF30w	NOP

	ADDAx.xyzw	ACC, VF19, VF00x	NOP
	MADDAx.xyzw	ACC, VF20, VF26x	NOP
	MADDAy.xyzw	ACC, VF21, VF26y	NOP
	MADDz.xyzw	VF28, VF22, VF26z	NOP

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF12, VF23w	NOP
	MINIi.xyzw	VF28, VF28, I	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFIR.w	VF29w, VI02
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       

ALCTransform2_ContinueNW_ADC:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	NOP
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF30, VF10, VF23z	NOP

	NOP		MTIR	VI01, VF23w
	NOP		IAND	VI01, VI01, VI07
	MAXx.xyz	VF26, VF26, VF00	IADDIU	VI01, VI01, ALCTransform_ProcessInst - ALC_RefAddress

	CLIPw.xyz	VF30, VF30	IADD	VI01, VI01, VI09

	MULAx.xyzw	ACC, VF15, VF30x	MR32.z	VF24, VF00	; set 1.0
	MADDAy.xyzw	ACC, VF16, VF30y	MOVE	VF29, VF25	; copy xfrmed XYZ
	MADDAz.xyzw	ACC, VF17, VF30z	NOP
	MADDw.xyzw	VF25, VF18, VF30w	NOP

	ADDAx.xyzw	ACC, VF19, VF00x	NOP
	MADDAx.xyzw	ACC, VF20, VF26x	NOP
	MADDAy.xyzw	ACC, VF21, VF26y	NOP
	MADDz.xyzw	VF28, VF22, VF26z	NOP

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF12, VF23w	NOP
	MINIi.xyzw	VF28, VF28, I	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFIR.w	VF29w, VI08
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Transform Loop Process Instruction
;---------------------------------------------------------------------------------------------------------------					       
ALCTransform_ProcessInst:
	NOP		ILW.x	VI01, 0(VI12)x	; Load InstructionRoutine Offset
	NOP		ILW.y	VI02, 0(VI12)y
	NOP		ILW.z	VI03, 0(VI12)z
	NOP		IADDIU	VI12, VI12, 1
	NOP		JR	VI01	; Branch to InstructionRoutine
	NOP		IADD	VI13, VI02, VI10	; <ds>

;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       

ALCTransform1_Continue:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	FCAND	VI01, 0x3ffff
	MADDAy.xyzw	ACC, VF02, VF23y	IADDIU	VI02, VI01, 0x7fff
	MADDz.xyzw	VF30, VF03, VF23z	NOP

	FTOI0.xyzw	VF28, VF28	SQI	VF27, (VI06++)	; store STQ
	FTOI4.xyz	VF29, VF29	NOP

	MAXx.xyz	VF26, VF26, VF00x	NOP

	CLIPw.xyz	VF30, VF30	NOP

	MULAx.xyzw	ACC, VF15, VF30x	SQI	VF28, (VI06++)	; set 1.0
	MADDAy.xyzw	ACC, VF16, VF30y	MR32.z	VF24, VF00
	MADDAz.xyzw	ACC, VF17, VF30z	SQI	VF29, (VI06++)
	MADDw.xyzw	VF25, VF18, VF30w	MOVE	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MTIR	VI01, VF23w
	MADDAx.xyzw	ACC, VF20, VF26x	IAND	VI01, VI01, VI07
	MADDAy.xyzw	ACC, VF21, VF26y	IADDIU	VI01, VI01, ALCTransform_ProcessInst - ALC_RefAddress
	MADDz.xyzw	VF28, VF22, VF26z	IADD	VI01, VI01, VI09

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF05, VF23w	NOP
	MINIi.xyzw	VF28, VF28, I	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFIR.w	VF29w, VI02
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       

ALCTransform1_Continue_ADC:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	NOP
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF30, VF03, VF23z	NOP

	FTOI0.xyzw	VF28, VF28	SQI	VF27, (VI06++)
	FTOI4.xyz	VF29, VF29	NOP

	MAXx.xyz	VF26, VF26, VF00	NOP

	CLIPw.xyz	VF30, VF30	NOP

	MULAx.xyzw	ACC, VF15, VF30x	SQI	VF28, (VI06++)	; set 1.0
	MADDAy.xyzw	ACC, VF16, VF30y	MR32.z	VF24, VF00
	MADDAz.xyzw	ACC, VF17, VF30z	SQI	VF29, (VI06++)
	MADDw.xyzw	VF25, VF18, VF30w	MOVE	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MTIR	VI01, VF23w
	MADDAx.xyzw	ACC, VF20, VF26x	IAND	VI01, VI01, VI07
	MADDAy.xyzw	ACC, VF21, VF26y	IADDIU	VI01, VI01, ALCTransform_ProcessInst - ALC_RefAddress
	MADDz.xyzw	VF28, VF22, VF26z	IADD	VI01, VI01, VI09

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF05, VF23w	NOP
	MINIi.xyzw	VF28, VF28, I	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	MFIR.w	VF29w, VI08
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP


;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline - Matrix 2
;---------------------------------------------------------------------------------------------------------------					       

ALCTransform2_Continue:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	FCAND	VI01, 0x3ffff
	MADDAy.xyzw	ACC, VF09, VF23y	IADDIU	VI02, VI01, 0x7fff
	MADDz.xyzw	VF30, VF10, VF23z	NOP

	FTOI0.xyzw	VF28, VF28	SQI	VF27, (VI06++)	; store STQ
	FTOI4.xyz	VF29, VF29	NOP

	MAXx.xyz	VF26, VF26, VF00x	NOP

	CLIPw.xyz	VF30, VF30	NOP

	MULAx.xyzw	ACC, VF15, VF30x	SQI	VF28, (VI06++)	; set 1.0
	MADDAy.xyzw	ACC, VF16, VF30y	MR32.z	VF24, VF00
	MADDAz.xyzw	ACC, VF17, VF30z	SQI	VF29, (VI06++)
	MADDw.xyzw	VF25, VF18, VF30w	MOVE	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MTIR	VI01, VF23w
	MADDAx.xyzw	ACC, VF20, VF26x	IAND	VI01, VI01, VI07
	MADDAy.xyzw	ACC, VF21, VF26y	IADDIU	VI01, VI01, ALCTransform_ProcessInst - ALC_RefAddress
	MADDz.xyzw	VF28, VF22, VF26z	IADD	VI01, VI01, VI09

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF12, VF23w	NOP
	MINIi.xyzw	VF28, VF28, I	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFIR.w	VF29w, VI02
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       

ALCTransform2_Continue_ADC:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	NOP
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF30, VF10, VF23z	NOP

	FTOI0.xyzw	VF28, VF28	SQI	VF27, (VI06++)
	FTOI4.xyz	VF29, VF29	NOP

	MAXx.xyz	VF26, VF26, VF00	NOP

	CLIPw.xyz	VF30, VF30	NOP

	MULAx.xyzw	ACC, VF15, VF30x	SQI	VF28, (VI06++)
	MADDAy.xyzw	ACC, VF16, VF30y	MR32.z	VF24, VF00
	MADDAz.xyzw	ACC, VF17, VF30z	SQI	VF29, (VI06++)
	MADDw.xyzw	VF25, VF18, VF30w	MOVE	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MTIR	VI01, VF23w
	MADDAx.xyzw	ACC, VF20, VF26x	IAND	VI01, VI01, VI07
	MADDAy.xyzw	ACC, VF21, VF26y	IADDIU	VI01, VI01, ALCTransform_ProcessInst - ALC_RefAddress
	MADDz.xyzw	VF28, VF22, VF26z	IADD	VI01, VI01, VI09

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF12, VF23w	NOP
	MINIi.xyzw	VF28, VF28, I	LQI	VF23, (VI04++)
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	MFIR.w	VF29w, VI08
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline w/o Read - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       

ALCTransform1_ContinueNR:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	FCAND	VI01, 0x3ffff
	MADDAy.xyzw	ACC, VF02, VF23y	IADDIU	VI02, VI01, 0x7fff
	MADDz.xyzw	VF30, VF03, VF23z	NOP

	FTOI0.xyzw	VF28, VF28	SQI	VF27, (VI06++)	; store STQ
	FTOI4.xyz	VF29, VF29	NOP
	MAXx.xyz	VF26, VF26, VF00x	NOP

	CLIPw.xyz	VF30, VF30	NOP

	MULAx.xyzw	ACC, VF15, VF30x	SQI	VF28, (VI06++)
	MADDAy.xyzw	ACC, VF16, VF30y	MR32.z	VF24, VF00
	MADDAz.xyzw	ACC, VF17, VF30z	SQI	VF29, (VI06++)
	MADDw.xyzw	VF25, VF18, VF30w	MOVE	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MTIR	VI01, VF23w
	MADDAx.xyzw	ACC, VF20, VF26x	IAND	VI01, VI01, VI07
	MADDAy.xyzw	ACC, VF21, VF26y	IADDIU	VI01, VI01, ALCTransform_Finish - ALC_RefAddress
	MADDz.xyzw	VF28, VF22, VF26z	IADD	VI01, VI01, VI09

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF05, VF23w	MFIR.w	VF29w, VI02
	MINIi.xyzw	VF28, VF28, I	NOP
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	NOP
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       

ALCTransform1_ContinueNR_ADC:
	MULAw.xyzw	ACC, VF04, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF01, VF23x	NOP
	MADDAy.xyzw	ACC, VF02, VF23y	NOP
	MADDz.xyzw	VF30, VF03, VF23z	NOP

	FTOI0.xyzw	VF28, VF28	SQI	VF27, (VI06++)
	FTOI4.xyz	VF29, VF29	NOP

	MAXx.xyz	VF26, VF26, VF00	NOP

	CLIPw.xyz	VF30, VF30	NOP

	MULAx.xyzw	ACC, VF15, VF30x	SQI	VF28, (VI06++)
	MADDAy.xyzw	ACC, VF16, VF30y	MR32.z	VF24, VF00
	MADDAz.xyzw	ACC, VF17, VF30z	SQI	VF29, (VI06++)
	MADDw.xyzw	VF25, VF18, VF30w	MOVE	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MTIR	VI01, VF23w
	MADDAx.xyzw	ACC, VF20, VF26x	IAND	VI01, VI01, VI07
	MADDAy.xyzw	ACC, VF21, VF26y	IADDIU	VI01, VI01, ALCTransform_Finish - ALC_RefAddress
	MADDz.xyzw	VF28, VF22, VF26z	IADD	VI01, VI01, VI09

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF05, VF23w	MFIR.w	VF29w, VI08
	MINIi.xyzw	VF28, VF28, I	NOP
	MADDAz.xyz	ACC, VF06, VF24z	JR	VI01
	MADDw.xyz	VF26, VF07, VF24w	NOP
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Continue the Vertex Pipeline w/o Read - Matrix 2
;---------------------------------------------------------------------------------------------------------------					       

ALCTransform2_ContinueNR:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	FCAND	VI01, 0x3ffff
	MADDAy.xyzw	ACC, VF09, VF23y	IADDIU	VI02, VI01, 0x7fff
	MADDz.xyzw	VF30, VF10, VF23z	NOP

	FTOI0.xyzw	VF28, VF28	SQI	VF27, (VI06++)	; store STQ
	FTOI4.xyz	VF29, VF29	NOP

	MAXx.xyz	VF26, VF26, VF00x	NOP

	CLIPw.xyz	VF30, VF30	NOP

	MULAx.xyzw	ACC, VF15, VF30x	SQI	VF28, (VI06++)
	MADDAy.xyzw	ACC, VF16, VF30y	MR32.z	VF24, VF00
	MADDAz.xyzw	ACC, VF17, VF30z	SQI	VF29, (VI06++)
	MADDw.xyzw	VF25, VF18, VF30w	MOVE	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MTIR	VI01, VF23w
	MADDAx.xyzw	ACC, VF20, VF26x	IAND	VI01, VI01, VI07
	MADDAy.xyzw	ACC, VF21, VF26y	IADDIU	VI01, VI01, ALCTransform_Finish - ALC_RefAddress
	MADDz.xyzw	VF28, VF22, VF26z	IADD	VI01, VI01, VI09

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF12, VF23w	MFIR.w	VF29w, VI02
	MINIi.xyzw	VF28, VF28, I	NOP
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	NOP
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       

ALCTransform2_ContinueNR_ADC:
	MULAw.xyzw	ACC, VF11, VF00w	DIV	Q, VF00w, VF25w
	MADDAx.xyzw	ACC, VF08, VF23x	NOP
	MADDAy.xyzw	ACC, VF09, VF23y	NOP
	MADDz.xyzw	VF30, VF10, VF23z	NOP

	FTOI0.xyzw	VF28, VF28	SQI	VF27, (VI06++)
	FTOI4.xyz	VF29, VF29	NOP

	MAXx.xyz	VF26, VF26, VF00	NOP

	CLIPw.xyz	VF30, VF30	NOP

	MULAx.xyzw	ACC, VF15, VF30x	SQI	VF28, (VI06++)
	MADDAy.xyzw	ACC, VF16, VF30y	MR32.z	VF24, VF00
	MADDAz.xyzw	ACC, VF17, VF30z	SQI	VF29, (VI06++)
	MADDw.xyzw	VF25, VF18, VF30w	MOVE	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MTIR	VI01, VF23w
	MADDAx.xyzw	ACC, VF20, VF26x	IAND	VI01, VI01, VI07
	MADDAy.xyzw	ACC, VF21, VF26y	IADDIU	VI01, VI01, ALCTransform_Finish - ALC_RefAddress
	MADDz.xyzw	VF28, VF22, VF26z	IADD	VI01, VI01, VI09

	MULq.xyz	VF27, VF24, Q	LQI	VF24, (VI04++)
	MULq.xyz	VF29, VF29, Q	NOP

	MULAw.xyz	ACC, VF12, VF23w	MFIR.w	VF29w, VI08
	MINIi.xyzw	VF28, VF28, I	NOP
	MADDAz.xyz	ACC, VF13, VF24z	JR	VI01
	MADDw.xyz	VF26, VF14, VF24w	NOP
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP

;---------------------------------------------------------------------------------------------------------------					       
; Finish the Pipeline - Matrix 1
;---------------------------------------------------------------------------------------------------------------					       

ALCTransform_Finish:
	FTOI4.xyz	VF29, VF29	FCAND	VI01, 0x3ffff
	FTOI0.xyzw	VF28, VF28	IADDIU	VI02, VI01, 0x7fff
	NOP		SQI	VF27, (VI06++)

	MAXx.xyz	VF26, VF26, VF00	DIV	Q, VF00w, VF25w
	NOP		MR32.z	VF24, VF00
	NOP		SQI	VF28, (VI06++)
	NOP		SQI	VF29, (VI06++)
	NOP		MOVE.xyz	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MFIR.w	VF29w, VI02
	MADDAx.xyzw	ACC, VF20, VF26x	ILW.x	VI01, 0(VI12)x
	MADDAy.xyzw	ACC, VF21, VF26y	ILW.y	VI02, 0(VI12)y
	MADDz.xyzw	VF28, VF22, VF26z	ILW.z	VI03, 0(VI12)z

	MULq.xyz	VF29, VF29, Q	IADDIU	VI12, VI12, 1
	MULq.xyz	VF27, VF24, Q	NOP
	MINIi.xyzw	VF28, VF28, I	NOP
	FTOI0.xyzw	VF28, VF28	NOP
	FTOI4.xyz	VF29, VF29	NOP
	NOP		NOP
	NOP		SQI	VF27, (VI06++)
	NOP		SQI	VF28, (VI06++)
	NOP		JR	VI01
	NOP		SQI	VF29, (VI06++)
;---------------------------------------------------------------------------------------------------------------					       
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP
	NOP		NOP

ALCTransform_Finish_ADC:
	FTOI4.xyz	VF29, VF29	DIV	Q, VF00w, VF25w
	FTOI0.xyzw	VF28, VF28	MR32.z	VF24, VF00
	NOP		SQI	VF27, (VI06++)
	MAXx.xyz	VF26, VF26, VF00	NOP
	NOP		NOP

	NOP		SQI	VF28, (VI06++)
	NOP		SQI	VF29, (VI06++)
	NOP		MOVE.xyz	VF29, VF25

	ADDAx.xyzw	ACC, VF19, VF00x	MFIR.w	VF29, VI08
	MADDAx.xyzw	ACC, VF20, VF26x	ILW.x	VI01, 0(VI12)x
	MADDAy.xyzw	ACC, VF21, VF26y	ILW.y	VI02, 0(VI12)y
	MADDz.xyzw	VF28, VF22, VF26z	ILW.z	VI03, 0(VI12)z

	MULq.xyz	VF29, VF29, Q	IADDIU	VI12, VI12, 1
	MULq.xyz	VF27, VF24, Q	NOP
	MINIi.xyzw	VF28, VF28, I	NOP
	FTOI0.xyzw	VF28, VF28	NOP
	FTOI4.xyz	VF29, VF29	NOP
	NOP		NOP
	NOP		SQI	VF27, (VI06++)
	NOP		SQI	VF28, (VI06++)
	NOP		JR	VI01
	NOP		SQI	VF29, (VI06++)


;---------------------------------------------------------------------------------------------------------------					       

dbAnimLC_Start:
	NOP		XTOP	VI01

	NOP		LQ	VF09, 0(VI01)	; Common->Screen Mtx 1
	NOP		LQ	VF10, 1(VI01)	; Common->Screen Mtx 2
	NOP		LQ	VF11, 2(VI01)	; Common->Screen Mtx 3
	NOP		LQ	VF12, 3(VI01)	; Common->Screen Mtx 4
	
	NOP		LQ	VF05, 4(VI01)	; Obj->Common Mtx 1
	NOP		LQ	VF06, 5(VI01)	; Obj->Common Mtx 2
	NOP		LQ	VF07, 6(VI01)	; Obj->Common Mtx 3
	NOP		LQ	VF08, 7(VI01)	; Obj->Common Mtx 4


	NOP		LQ	VF19,11(VI01)	; Light Ambient
	NOP		LQ	VF20, 8(VI01)	; Light Color Mtx
	NOP		LQ	VF21, 9(VI01)
	NOP		LQ	VF22,10(VI01)

	MULx.xyzw	VF04, VF00, VF00	LQ	VF02,12(VI01)	; Load Cull-Screen Mtx
	NOP		LQ	VF03,13(VI01)

	NOP		LQ	VF31,14(VI01)	; Load GIFTag

ALC_ConcatMatrices:
	MULAx.xyzw	ACC, VF09, VF05x	NOP
	MADDAy.xyzw	ACC, VF10, VF05y	NOP
	MADDAz.xyzw	ACC, VF11, VF05z	NOP
	MADDw.xyzw	VF26, VF12, VF05w	NOP

	MULAx.xyzw	ACC, VF09, VF06x	NOP
	MADDAy.xyzw	ACC, VF10, VF06y	NOP
	MADDAz.xyzw	ACC, VF11, VF06z	NOP
	MADDw.xyzw	VF27, VF12, VF06w	NOP

	MULAx.xyzw	ACC, VF09, VF07x	NOP
	MADDAy.xyzw	ACC, VF10, VF07y	NOP
	MADDAz.xyzw	ACC, VF11, VF07z	NOP
	MADDw.xyzw	VF28, VF12, VF07w	NOP
	
	MULAx.xyzw	ACC, VF09, VF08x	NOP
	MADDAy.xyzw	ACC, VF10, VF08y	NOP
	MADDAz.xyzw 	ACC, VF11, VF08z	NOP
	MADDw.xyzw	VF29, VF12, VF08w	NOP

ALC_BranchFirstBuffer:
	NOP		SQ	VF26, 0(VI00)
	NOP		SQ	VF27, 1(VI00)
	NOP		SQ	VF28, 2(VI00)
	NOP		SQ	VF29, 3(VI00)

ALC_SetupPersistantData:
	NOP		MOVE.x	VF15, VF03
	NOP		MOVE.yzw	VF15, VF04

	NOP		MOVE.y	VF16, VF03
	NOP		MOVE.xzw	VF16, VF04

	NOP		MOVE.z	VF17, VF03
	ADDw.z	VF18, VF00, VF03	MOVE.xyw	VF17, VF04

	NOP		MOVE.xy	VF18, VF02
	NOP		MOVE.w	VF18, VF00

	NOP[E]		B	ALCStart
	NOP		NOP		; Grab Buffer Base

;--------------------------------------------------------------------------------------------------
define( 'MatrixSize', eval( 9))

define( 'irTemp1', 'VI01')
define( 'irCurrentBit', 'VI06')

define( 'irMatrixAddr', 'VI02')
define( 'irDirtyMtxCnt', 'VI03')
define( 'irDirtyMtxBits1', 'VI04')
define( 'irDirtyMtxBits2', 'VI05')
define( 'irReturnAddr', 'VI14')

include( 'AnimMatrixCheck.vm4')
;--------------------------------------------------------------------------------------------------

.enddmadata
dbAnimLC_CodeEnd:


